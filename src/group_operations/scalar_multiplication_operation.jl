"""
    ScalarMultiplicationGroupOperation <: AbstractMultiplicationGroupOperation

A group operation that is realised by a scalar multiplication.
"""
struct ScalarMultiplicationGroupOperation <: AbstractMultiplicationGroupOperation end

function get_number_type(x::Number)
    return typeof(x)
end

function get_number_type(x::Array{<:Number,0})
    return typeof(x[])
end

function get_number_type(x::Ref{<:Number})
    return typeof(x[])
end

get_num(x::Number) = identity(x)
function get_num(x::Array{<:Number,0})
    return x[]
end
function get_num(x::Ref{<:Number})
    return x[]
end

function compose(
    ::LieGroup{ð”½,<:ScalarMultiplicationGroupOperation}, g::Number, h::Number
) where {ð”½}
    return g * h
end
function compose(
    ::LieGroup{ð”½,<:ScalarMultiplicationGroupOperation},
    g::Union{<:Number,Ref{<:Number},Array{<:Number,0}},
    h::Union{<:Number,Ref{<:Number},Array{<:Number,0}},
) where {ð”½}
    return get_num(g) * get_num(h)
end

function compose!(
    ::LieGroup{ð”½,<:ScalarMultiplicationGroupOperation},
    k::Union{Ref{T},Array{T,0}},
    g::T1,
    h::T2,
) where {ð”½,T1<:Number,T2<:Number,T<:Number}
    k[] = ((k === g || k === h) ? copy(g * h) : g * h)
    return k
end

function compose!(
    ::LieGroup{ð”½,<:ScalarMultiplicationGroupOperation},
    k::Union{Ref{T},Array{T,0}},
    g::Union{Ref{T1},Array{T1,0}},
    h::T2,
) where {ð”½,T1<:Number,T2<:Number,T<:Number}
    k[] = ((k === g || k === h) ? copy(g[] * h) : g[] * h)
    return k
end

function compose!(
    ::LieGroup{ð”½,<:ScalarMultiplicationGroupOperation},
    k::Union{Ref{T},Array{T,0}},
    g::T1,
    h::Union{Ref{T2},Array{T2,0}},
) where {ð”½,T1<:Number,T2<:Number,T<:Number}
    k[] = ((k === g || k === h) ? copy(g * h[]) : g * h[])
    return k
end

function compose!(
    ::LieGroup{ð”½,<:ScalarMultiplicationGroupOperation},
    k::Union{Ref{T},Array{T,0}},
    g::Union{Ref{T1},Array{T1,0}},
    h::Union{Ref{T2},Array{T2,0}},
) where {ð”½,T1<:Number,T2<:Number,T<:Number}
    k[] = ((k === g || k === h) ? copy(g[] * h[]) : g[] * h[])
    return k
end

function conjugate(
    ::LieGroup{ð”½,<:ScalarMultiplicationGroupOperation}, g::Number, h::Number
) where {ð”½}
    return g * h * inv(g)
end

function conjugate(
    ::LieGroup{ð”½,<:ScalarMultiplicationGroupOperation},
    g::Union{<:Number,Ref{<:Number},Array{<:Number,0}},
    h::Union{<:Number,Ref{<:Number},Array{<:Number,0}},
) where {ð”½}
    return get_num(g) * get_num(h) * inv(get_num(g))
end

_doc_exp_scalar_mult = """
    exp(G::LieGroup{ð”½,ScalarMultiplicationGroupOperation}, e::Identity{ScalarMultiplicationGroupOperation}, X, t::Number=1)
    exp!(G::LieGroup{ð”½,ScalarMultiplicationGroupOperation}, g, e::Identity{ScalarMultiplicationGroupOperation}, X, t::Number=1)

Compute the Lie group exponential on a [`LieGroup`](@ref) with a [`ScalarMultiplicationGroupOperation`](@ref),
which simplifies to the [ordinary exponential](https://en.wikipedia.org/wiki/Matrix_exponential).

This can be computed in-place of `g`.
"""

@doc "$(_doc_exp_scalar_mult)"
ManifoldsBase.exp(
    ::LieGroup{ð”½,ScalarMultiplicationGroupOperation},
    X::Union{<:Number,Ref{<:Number},<:Array{<:Number,0}},
    t::Number=1,
) where {ð”½} = fill(exp(t * (X isa Number ? X : X[])))

@doc "$(_doc_exp_scalar_mult)"
function ManifoldsBase.exp!(
    ::LieGroup{ð”½,ScalarMultiplicationGroupOperation},
    g::Union{Ref{<:Number},<:Array{<:Number,0}},
    X::Union{<:Number,Ref{<:Number},<:Array{<:Number,0}},
    t::Number=1,
) where {ð”½}
    g[] = exp(t * (X isa Number ? X : X[]))
    return g
end

Base.inv(::LieGroup{ð”½,<:ScalarMultiplicationGroupOperation}, g::Number) where {ð”½} = inv(g)
function Base.inv(
    ::LieGroup{ð”½,<:ScalarMultiplicationGroupOperation},
    g::Union{Ref{<:Number},Array{<:Number,0}},
) where {ð”½}
    return inv(g[])
end

function inv!(
    ::LieGroup{ð”½,<:ScalarMultiplicationGroupOperation},
    h::Union{Ref{<:Number},Array{<:Number,0}},
    g::Union{<:Number,Ref{<:Number},Array{<:Number,0}},
) where {ð”½}
    h[] = inv(get_num(h))
    return h
end

function inv_left_compose(
    ::LieGroup{ð”½,ScalarMultiplicationGroupOperation}, g::Number, h::Number
) where {ð”½}
    return inv(g) * h
end
function inv_left_compose(
    ::LieGroup{ð”½,ScalarMultiplicationGroupOperation},
    g::Union{<:Number,Ref{<:Number},Array{<:Number,0}},
    h::Union{<:Number,Ref{<:Number},Array{<:Number,0}},
) where {ð”½}
    return inv(get_num(g)) * get_num(h)
end

function inv_left_compose!(
    G::LieGroup{ð”½,<:ScalarMultiplicationGroupOperation},
    k::Union{Ref{<:Number},Array{<:Number,0}},
    g::Union{<:Number,Ref{<:Number},Array{<:Number,0}},
    h::Union{<:Number,Ref{<:Number},Array{<:Number,0}},
) where {ð”½}
    k[] = inv_left_compose(G, g, h)
    return k
end

function inv_right_compose(
    ::LieGroup{ð”½,ScalarMultiplicationGroupOperation}, g::Number, h::Number
) where {ð”½}
    return g * inv(h)
end
function inv_right_compose(
    ::LieGroup{ð”½,ScalarMultiplicationGroupOperation},
    g::Union{<:Number,Ref{<:Number},Array{<:Number,0}},
    h::Union{<:Number,Ref{<:Number},Array{<:Number,0}},
) where {ð”½}
    return get_num(g) * inv(get_num(h))
end

function inv_right_compose!(
    G::LieGroup{ð”½,<:ScalarMultiplicationGroupOperation},
    k::Union{Ref{<:Number},Array{<:Number,0}},
    g::Union{<:Number,Ref{<:Number},Array{<:Number,0}},
    h::Union{<:Number,Ref{<:Number},Array{<:Number,0}},
) where {ð”½}
    k[] = inv_right_compose(G, g, h)
    return k
end

_doc_identity_element_scalar_mult = """
    identity_element(G::LieGroup{ð”½,ScalarMultiplicationGroupOperation})
    identity_element!(G::LieGroup{ð”½,ScalarMultiplicationGroupOperation}, e)

Return the a point representation of the [`Identity`](@ref),
which for an [`ScalarMultiplicationGroupOperation`](@ref) is the one-element.
"""

@doc "$(_doc_identity_element_scalar_mult)"
identity_element(::LieGroup{ð”½,ScalarMultiplicationGroupOperation}) where {ð”½} = 1.0
function identity_element(
    ::LieGroup{ð”½,ScalarMultiplicationGroupOperation}, e::T
) where {ð”½,T<:Number}
    return one(e)
end

@doc "$(_doc_identity_element_scalar_mult)"
identity_element!(::LieGroup{ð”½,ScalarMultiplicationGroupOperation}, e) where {ð”½}
function identity_element!(::LieGroup{ð”½,ScalarMultiplicationGroupOperation}, e) where {ð”½}
    return fill!(e, 1)
end

"""
    AbelianMultiplicationGroupOperation <: AbstractMultiplicationGroupOperation

A group operation that is realised by an abelian multiplication.
"""
struct AbelianMultiplicationGroupOperation <: AbstractMultiplicationGroupOperation end

function _compose(
    ::LieGroup{ğ”½,<:AbelianMultiplicationGroupOperation}, g::Number, h::Number
) where {ğ”½}
    return g * h
end

function _compose(
    G::LieGroup{ğ”½,<:AbelianMultiplicationGroupOperation},
    p::AbstractArray{<:Any,0},
    q::AbstractArray{<:Any,0},
) where {ğ”½}
    return map((pp, qq) -> compose(G, pp, qq), p, q)
end

function _compose(
    ::LieGroup{ğ”½,<:AbelianMultiplicationGroupOperation},
    g::Number,
    h::AbstractArray{<:Any,0},
) where {ğ”½}
    return g .* h
end

function _compose(
    ::LieGroup{ğ”½,<:AbelianMultiplicationGroupOperation},
    g::AbstractArray{<:Any,0},
    h::Number,
) where {ğ”½}
    return g .* h
end

function _compose!(G::LieGroup{ğ”½,<:AbelianMultiplicationGroupOperation}, k, g, h) where {ğ”½}
    return copyto!(k, compose(G, g, h))
end

function conjugate(::LieGroup{ğ”½,<:AbelianMultiplicationGroupOperation}, g, h) where {ğ”½}
    return g
end

function conjugate!(::LieGroup{ğ”½,<:AbelianMultiplicationGroupOperation}, k, g, h) where {ğ”½}
    return copyto!(k, g)
end

_doc_diff_conjugate_abelmul = """
    diff_conjugate(G::LieGroup{ğ”½,<:AbelianMultiplicationGroupOperation}, g, h, X)
    diff_conjugate!(G::LieGroup{ğ”½,<:AbelianMultiplicationGroupOperation}, Y, g, h, X)

Compute the differential of the conjugate ``c_g(h) = g$(_math(:âˆ˜))h$(_math(:âˆ˜))g^{-1} = ghg^{-1}``,
which simplifies for an [`AbelianMultiplicationGroupOperation`](@ref) to ``D(c_g(h))[X] = X``.

This can be computed in-place of `Y` if `Y` is `mutable`.
"""

@doc "$(_doc_diff_conjugate_abelmul)"
function diff_conjugate(
    ::LieGroup{ğ”½,<:AbelianMultiplicationGroupOperation}, g, h, X::Number
) where {ğ”½}
    return X
end

function diff_conjugate(
    G::LieGroup{ğ”½,<:AbelianMultiplicationGroupOperation},
    g::AbstractArray{<:Any,0},
    h::AbstractArray{<:Any,0},
    X::AbstractArray{<:Any,0},
) where {ğ”½}
    return map(XX -> diff_conjugate(G, g, h, XX), X)
end

@doc "$(_doc_diff_conjugate_abelmul)"
function diff_conjugate!(
    G::LieGroup{ğ”½,<:AbelianMultiplicationGroupOperation},
    Y::AbstractArray{<:Any,0},
    g::AbstractArray{<:Any,0},
    h::AbstractArray{<:Any,0},
    X::AbstractArray{<:Any,0},
) where {ğ”½}
    return copyto!(LieAlgebra(G), Y, diff_conjugate(G, g, h, X))
end

_doc_diff_inv_abelmult = """
    diff_inv(G::LieGroup{ğ”½,<:AbelianMultiplicationGroupOperation}, g, X)
    diff_inv!(G::LieGroup{ğ”½,<:AbelianMultiplicationGroupOperation}, Y, g, X)

Compute the value of the differential ``DÎ¹_{$(_math(:G))}(g)[X]`` of the inversion ``Î¹_{$(_math(:G))}(g) := g^{-1}`` at ``X âˆˆ ğ”¤``
in the [`LieAlgebra`](@ref) ``ğ”¤`` of the [`LieGroup`](@ref) `G`.

In the abelian case, the computation simplifies to

```math
DÎ¹_{$(_math(:G))}(g)[X] = -gXg^{-1} = -X.
```

This can be computed in-place of `Y` if `Y` is `mutable`.
"""

@doc "$(_doc_diff_inv_abelmult)"
function diff_inv!(G::LieGroup{ğ”½,<:AbelianMultiplicationGroupOperation}, Y, g, X) where {ğ”½}
    return copyto!(LieAlgebra(G), Y, -X)
end

_doc_diff_left_compose_abelmult = """
    diff_left_compose(G::LieGroup{ğ”½,<:AbelianMultiplicationGroupOperation}, g, h, X)
    diff_left_compose!(G::LieGroup{ğ”½,<:AbelianMultiplicationGroupOperation}, Y, g, h, X)

Compute the differential of the left group multiplication ``Î»_g(h) = g$(_math(:âˆ˜))h``.

Due to differences in the representation of some abelian Lie groups, this method wraps a concrete implementation of a specific abelian LieGroup with inputs of type `AbstractArray{<:Any,0}` and supports in-place computation.

This can be computed in-place of `Y` if `Y` is `mutable`.
"""#

@doc "$(_doc_diff_left_compose_abelmult)"
function diff_left_compose(
    G::LieGroup{ğ”½,<:AbelianMultiplicationGroupOperation},
    g::AbstractArray{<:Any,0},
    h,
    X::AbstractArray{<:Any,0},
) where {ğ”½}
    return map((gg, XX) -> diff_left_compose(G, gg, h, XX), g, X)
end

@doc "$(_doc_diff_left_compose_abelmult)"
function diff_left_compose!(
    G::LieGroup{ğ”½,<:AbelianMultiplicationGroupOperation}, Y, g, h, X
) where {ğ”½}
    return copyto!(LieAlgebra(G), Y, diff_left_compose(G, g, h, X))
end

_doc_diff_right_compose_abelmult = """
    diff_right_compose(G::LieGroup{ğ”½,<:AbelianMultiplicationGroupOperation}, g, h, X)
    diff_right_compose!(G::LieGroup{ğ”½,<:AbelianMultiplicationGroupOperation}, Y, g, h, X)

Compute the differential of the right group multiplication ``Ï_g(h) = h$(_math(:âˆ˜))g``.

Due to differences in the representation of some abelian Lie groups, this method wraps a concrete implementation of a specific abelian LieGroup with inputs of type `AbstractArray{<:Any,0}` and supports in-place computation.

This can be computed in-place of `Y` if `Y` is `mutable`.
"""#

@doc "$(_doc_diff_right_compose_abelmult)"
function diff_right_compose(
    G::LieGroup{ğ”½,<:AbelianMultiplicationGroupOperation},
    g::AbstractArray{<:Any,0},
    h,
    X::AbstractArray{<:Any,0},
) where {ğ”½}
    return map((gg, XX) -> diff_right_compose(G, gg, h, XX), g, X)
end

@doc "$(_doc_diff_right_compose_abelmult)"
function diff_right_compose!(
    G::LieGroup{ğ”½,<:AbelianMultiplicationGroupOperation}, Y, g, h, X
) where {ğ”½}
    return copyto!(LieAlgebra(G), Y, diff_right_compose(G, g, h, X))
end

_doc_exp_abelmult = """
    exp(G::LieGroup{ğ”½,<:AbelianMultiplicationGroupOperation}, X)
    exp(G::LieGroup{ğ”½,<:AbelianMultiplicationGroupOperation}, g, X)
    exp!(G::LieGroup{ğ”½,<:AbelianMultiplicationGroupOperation}, h, X)
    exp!(G::LieGroup{ğ”½,<:AbelianMultiplicationGroupOperation}, h, g, X)

Compute the Lie group exponential on a [`LieGroup`](@ref) at a point `g` or the [`Identity`](@ref) with an [`AbelianMultiplicationGroupOperation`](@ref).

Due to differences in the representation of some abelian Lie groups, this method wraps a concrete implementation of a specific abelian LieGroup with inputs of type `AbstractArray{<:Any,0}` and supports in-place computation.

This can be computed in-place of `h` if `h` is `mutable`.
"""

@doc "$(_doc_exp_abelmult)"
function ManifoldsBase.exp(
    G::LieGroup{ğ”½,AbelianMultiplicationGroupOperation}, X::AbstractArray{<:Any,0}
) where {ğ”½}
    return map(XX -> exp(G, XX), X)
end

function ManifoldsBase.exp(
    G::LieGroup{ğ”½,AbelianMultiplicationGroupOperation}, g, X
) where {ğ”½}
    return compose(G, g, exp(G, X))
end

function ManifoldsBase.exp(
    G::LieGroup{ğ”½,AbelianMultiplicationGroupOperation},
    g::AbstractArray{<:Any,0},
    X::AbstractArray{<:Any,0},
) where {ğ”½}
    return map((gg, XX) -> gg * exp(G, XX), g, X)
end

@doc "$(_doc_exp_abelmult)"
function ManifoldsBase.exp!(
    G::LieGroup{ğ”½,AbelianMultiplicationGroupOperation}, h, X
) where {ğ”½}
    return copyto!(h, exp(G, X))
end

function ManifoldsBase.exp!(
    G::LieGroup{ğ”½,AbelianMultiplicationGroupOperation}, h, g, X
) where {ğ”½}
    return copyto!(h, exp(G, g, X))
end

_doc_inv_abelmult = """
    inv(G::LieGroup{ğ”½,<:AbstractMultiplicationGroupOperation}, g)
    inv!(G::LieGroup{ğ”½,<:AbstractMultiplicationGroupOperation}, h, g)

Compute the inverse group element ``g^{-1}``, which for the [`AbelianMultiplicationGroupOperation`](@ref) simplifies for a scalar input to the ordinary scalar inverse ``g^{-1}``.

Due to differences in the representation of some abelian Lie groups, this method wraps a concrete implementation of a specific abelian LieGroup with inputs of type `AbstractArray{<:Any,0}` and supports in-place computation.

This can be computed in-place of `h` if `h` is `mutable`.
"""

@doc "$(_doc_inv_abelmult)"
Base.inv(::LieGroup{ğ”½,<:AbelianMultiplicationGroupOperation}, g::Number) where {ğ”½} = inv(g)

function Base.inv(
    G::LieGroup{ğ”½,<:AbelianMultiplicationGroupOperation}, g::AbstractArray{<:Any,0}
) where {ğ”½}
    return map(gg -> inv(G, gg), g)
end

@doc "$(_doc_inv_abelmult)"
function inv!(G::LieGroup{ğ”½,<:AbelianMultiplicationGroupOperation}, h, g) where {ğ”½}
    copyto!(h, inv(G, g))
    return h
end

function inv!(
    G::LieGroup{ğ”½,O}, g, ::Identity{O}
) where {ğ”½,O<:AbelianMultiplicationGroupOperation}
    return identity_element!(G, g)
end

function inv_left_compose(
    ::LieGroup{ğ”½,AbelianMultiplicationGroupOperation}, g::Number, h::Number
) where {ğ”½}
    return inv(g) * h
end

function inv_left_compose(
    ::LieGroup{ğ”½,AbelianMultiplicationGroupOperation},
    g::AbstractArray{<:Any,0},
    h::AbstractArray{<:Any,0},
) where {ğ”½}
    return map(\, g, h)
end

function inv_left_compose!(
    G::LieGroup{ğ”½,<:AbelianMultiplicationGroupOperation}, k, g, h
) where {ğ”½}
    return copyto!(k, inv_left_compose(G, g, h))
end

function inv_right_compose(
    ::LieGroup{ğ”½,AbelianMultiplicationGroupOperation}, g::Number, h::Number
) where {ğ”½}
    return g * inv(h)
end

function inv_right_compose(
    ::LieGroup{ğ”½,AbelianMultiplicationGroupOperation},
    g::AbstractArray{<:Any,0},
    h::AbstractArray{<:Any,0},
) where {ğ”½}
    return map(/, g, h)
end

function inv_right_compose!(
    G::LieGroup{ğ”½,<:AbelianMultiplicationGroupOperation}, k, g, h
) where {ğ”½}
    return copyto!(k, inv_right_compose(G, g, h))
end

_doc_identity_element_abelian_mult = """
    identity_element(G::LieGroup{ğ”½,AbelianMultiplicationGroupOperation})
    identity_element!(G::LieGroup{ğ”½,AbelianMultiplicationGroupOperation}, e)

Return the point representation of the [`Identity`](@ref),
which for an [`AbelianMultiplicationGroupOperation`](@ref) is the one-element.

This can be computed in `e` if `e` is `mutable`.
"""

@doc "$(_doc_identity_element_abelian_mult)"
function identity_element(
    ::LieGroup{ğ”½,AbelianMultiplicationGroupOperation}, ::Type{T}
) where {ğ”½,T<:Union{Number,AbstractArray{0,<:Number}}}
    return one(T)
end
function identity_element(
    ::LieGroup{ğ”½,AbelianMultiplicationGroupOperation}, ::Type{Array{T,0}}
) where {ğ”½,T<:Number}
    return fill(one(T))
end

@doc "$(_doc_identity_element_abelian_mult)"
identity_element!(::LieGroup{ğ”½,AbelianMultiplicationGroupOperation}, e) where {ğ”½}

function identity_element!(
    ::LieGroup{ğ”½,AbelianMultiplicationGroupOperation}, e::AbstractArray{<:Number,0}
) where {ğ”½}
    return fill!(e, 1)
end

_doc_lie_bracket_abelmult = """
    lie_bracket(::LieAlgebra{ğ”½,AbelianMultiplicationGroupOperation}, X, Y)
    lie_bracket!(::LieAlgebra{ğ”½,AbelianMultiplicationGroupOperation}, Z, X, Y)

Compute the Lie bracket ``[â‹…,â‹…]: $(_math(:ğ”¤))Ã—$(_math(:ğ”¤)) â†’ $(_math(:ğ”¤))``,
which for the for the [`AbelianMultiplicationGroupOperation`](@ref) yields the zero vector of the [`LieAlgebra`](@ref) due to commutativity.

```math
[X, Y] = XY-YX = 0
```

The computation can be done in-place of `Z` if `Z` is `mutable`.
"""

@doc "$(_doc_lie_bracket_abelmult)"
function lie_bracket(
    ::LieAlgebra{ğ”½,O,<:LieGroup{ğ”¾,O}}, X::Number, Y::Number
) where {ğ”½,ğ”¾,O<:AbelianMultiplicationGroupOperation}
    return zero(X)
end

@doc "$(_doc_lie_bracket_abelmult)"
function lie_bracket!(
    ::LieAlgebra{ğ”½,O,<:LieGroup{ğ”¾,O}}, Z, X, Y
) where {ğ”½,ğ”¾,O<:AbelianMultiplicationGroupOperation}
    return fill!(Z, 0)
end

_doc_log_abelmult = """
    log(G::LieGroup{ğ”½,<:AbelianMultiplicationGroupOperation}, h)
    log(G::LieGroup{ğ”½,<:AbelianMultiplicationGroupOperation}, g, h)
    log!(G::LieGroup{ğ”½,<:AbeliantMultiplicationGroupOperation}, X, g)
    log!(G::LieGroup{ğ”½,<:AbeliantMultiplicationGroupOperation}, X, g, h)

Compute the Lie group logarithm on a [`LieGroup`](@ref) at a point `g` or the [`Identity`](@ref) with a concrete instance of [`AbelianMultiplicationGroupOperation`](@ref).

Due to differences in the representation of some abelian Lie groups, this method wraps a concrete implementation of a specific abelian LieGroup with inputs of type `AbstractArray{<:Any,0}` and supports in-place computation.

This can be computed in-place of `X` if `X` is `mutable`.
"""

@doc "$(_doc_log_abelmult)"
function ManifoldsBase.log(
    G::LieGroup{ğ”½,AbelianMultiplicationGroupOperation}, g, h
) where {ğ”½}
    return log(G, compose(G, inv(G, g), h))
end

function ManifoldsBase.log(
    G::LieGroup{ğ”½,AbelianMultiplicationGroupOperation},
    ::Identity{AbelianMultiplicationGroupOperation},
) where {ğ”½}
    return zero_vector(LieAlgebra(G))
end

function ManifoldsBase.log(
    G::LieGroup{ğ”½,AbelianMultiplicationGroupOperation},
    ::Identity{AbelianMultiplicationGroupOperation},
    T::Type,
) where {ğ”½}
    return zero_vector(LieAlgebra(G), T)
end

@doc "$(_doc_log_abelmult)"
function ManifoldsBase.log!(
    G::LieGroup{ğ”½,AbelianMultiplicationGroupOperation}, X, g
) where {ğ”½}
    return copyto!(X, log(G, g))
end

function ManifoldsBase.log!(
    G::LieGroup{ğ”½,AbelianMultiplicationGroupOperation},
    X,
    ::Identity{AbelianMultiplicationGroupOperation},
) where {ğ”½}
    return zero_vector!(LieAlgebra(G), X)
end

"""
    AbstractMultiplicationGroupOperation <: AbstractGroupOperation

A group operation that is realised introducing defaults that fall back
to `*` being overloaded, for example
`_compose(G::LieGroup{ğ”½,<:AbstractMultiplicationGroupOperation}, a, b) = a * b`
"""
abstract type AbstractMultiplicationGroupOperation <: AbstractGroupOperation end

"""
    AbstractMultiplicationGroupOperation <: AbstractMultiplicationGroupOperation

A group operation that is realised by a matrix multiplication.
"""
struct MatrixMultiplicationGroupOperation <: AbstractMultiplicationGroupOperation end

"""
    ScalarMultiplicationGroupOperation <: AbstractMultiplicationGroupOperation

A group operation that is realised by the multiplication of scalars. (Usefull for the complex CircleGroup)
"""
struct ScalarMultiplicationGroupOperation <: AbstractMultiplicationGroupOperation end

Base.:*(::Identity{MatrixMultiplicationGroupOperation}, p::Union{AbstractMatrix,Number}) = p
function Base.:*(
    p::Union{AbstractMatrix,Number}, ::Identity{MatrixMultiplicationGroupOperation}
)
    return p
end
function Base.:*(
    e::Identity{<:AbstractMultiplicationGroupOperation},
    ::Identity{<:AbstractMultiplicationGroupOperation},
)
    return e
end
function Base.:*(
    e::Identity{<:AbstractMultiplicationGroupOperation}, ::Identity{AdditionGroupOperation}
)
    return e
end
function Base.:*(
    ::Identity{AdditionGroupOperation}, e::Identity{<:AbstractMultiplicationGroupOperation}
)
    return e
end

Base.:/(p, ::Identity{<:AbstractMultiplicationGroupOperation}) = p
Base.:/(::Identity{<:AbstractMultiplicationGroupOperation}, p) = inv(p)
function Base.:/(
    e::Identity{<:AbstractMultiplicationGroupOperation},
    ::Identity{<:AbstractMultiplicationGroupOperation},
)
    return e
end

Base.:\(p, ::Identity{<:AbstractMultiplicationGroupOperation}) = inv(p)
Base.:\(::Identity{<:AbstractMultiplicationGroupOperation}, p) = p
function Base.:\(
    e::Identity{<:AbstractMultiplicationGroupOperation},
    ::Identity{<:AbstractMultiplicationGroupOperation},
)
    return e
end

_doc_compose_mult = """
    compose(G::LieGroup{ğ”½,<:AbstractMultiplicationGroupOperation}, g, h)
    compose!(G::LieGroup{ğ”½,<:AbstractMultiplicationGroupOperation}, k, g, h)

Compute the group operation composition of `g` and `h` with respect to
an [`AbstractMultiplicationGroupOperation`](@ref) on `G`, which falls back to calling
`g*h`, where `*` is assumed to be overloaded accordingly.

This can be computed in-place of `k`.
"""

@doc "$(_doc_compose_mult)"
compose(::LieGroup{ğ”½,<:AbstractMultiplicationGroupOperation}, g, h) where {ğ”½}

@doc "$(_doc_compose_mult)"
compose!(::LieGroup{ğ”½,<:AbstractMultiplicationGroupOperation}, k, g, h) where {ğ”½}

function _compose!(::LieGroup{ğ”½,<:AbstractMultiplicationGroupOperation}, k, g, h) where {ğ”½}
    # perform the multiplication â€œsafeâ€, that is, even when providing
    # one of the inputs `g,h`` and as output `k`
    (k === g || k === h) ? copyto!(k, g * h) : mul!(k, g, h)
    return k
end

Base.inv(e::Identity{<:AbstractMultiplicationGroupOperation}) = e

LinearAlgebra.det(::Identity{<:AbstractMultiplicationGroupOperation}) = true

_doc_diff_conjugate_add = """
    diff_conjugate(G::LieGroup{ğ”½,<:AbstractMultiplicationGroupOperation}, g, h, X)
    diff_conjugate!(G::LieGroup{ğ”½,<:AbstractMultiplicationGroupOperation}, Y, g, h, X)

Compute the differential of the conjutage ``c_g(h) = g$(_math(:âˆ˜))h$(_math(:âˆ˜))g^{-1} = ghg^{-1}``,
which simplifies for an [`AbstractMultiplicationGroupOperation`](@ref) to ``D(c_g(h))[X] = gXg^{-1}``.
"""

@doc "$(_doc_diff_conjugate_add)"
diff_conjugate(G::LieGroup{ğ”½,<:AbstractMultiplicationGroupOperation}, g, h, X) where {ğ”½}

@doc "$(_doc_diff_conjugate_add)"
function diff_conjugate!(
    G::LieGroup{ğ”½,<:AbstractMultiplicationGroupOperation}, Y, g, h, X
) where {ğ”½}
    inv_right_compose!(G, Y, X, g) # Y = Xg^{-1}
    compose!(G, Y, g, Y) # Y = gY
    return Y
end

_doc_diff_inv_mult = """
    diff_inv(G::LieGroup{ğ”½,<:AbstractMultiplicationGroupOperation}, g, X)
    diff_inv!(G::LieGroup{ğ”½,<:AbstractMultiplicationGroupOperation}, Y, g, X)

Compute the value of differential ``DÎ¹_{$(_math(:G))}(g)[X]`` of matrix inversion ``Î¹_{$(_math(:G))}(g) := g^{-1}`` at ``X âˆˆ ğ”¤``
in the [`LieAlgebra`](@ref) ``ğ”¤`` of the [`LieGroup`](@ref) `G`.

The formula is given by

```math
DÎ¹_{$(_math(:G))}(g)[X] = -g^{$(_tex(:transp))}Xg^{-1},
```

which stems from using the differential of the inverse from [Giles:2008](@cite) given by
``D(g^{-1})[X] = -g^{-1}Xg^{-1}`` composed with the push forward of the left composition
``DÎ»_$(_math(:e))(g)[X] = gX`` mapping from the Liea algebra into the tangent space at ``g``,
and its adjoint ``D^*Î»_$(_math(:e))(g)[X] = g^{$(_tex(:transp))}X``.
Then we get ``g^{$(_tex(:transp))}(g^{-1}(gX)g^{-1})`` which simplifies to ``-g^{$(_tex(:transp))}Xg^{-1}`` from above.
"""

@doc "$(_doc_diff_inv_mult)"
diff_inv(::LieGroup{ğ”½,<:AbstractMultiplicationGroupOperation}, g, X) where {ğ”½}

function diff_inv(
    ::LieGroup{ğ”½,<:AbstractMultiplicationGroupOperation},
    p::AbstractArray{<:Number,0},
    X::AbstractArray{<:Number,0},
) where {ğ”½}
    p_inv = inv(p[])
    return -(p[] * X * p_inv)
end

@doc "$(_doc_diff_inv_mult)"
function diff_inv!(::LieGroup{ğ”½,<:AbstractMultiplicationGroupOperation}, Y, p, X) where {ğ”½}
    p_inv = inv(p)
    Z = X * p_inv
    mul!(Y, p', Z)
    Y .*= -1
    return Y
end

_doc_diff_left_compose_mult = """
    diff_left_compose(G::LieGroup{ğ”½,<:AbstractMultiplicationGroupOperation}, g, h, X)
    diff_left_compose!(G::LieGroup{ğ”½,<:AbstractMultiplicationGroupOperation}, Y, g, h, X)

Compute the differential of the left group multiplication ``Î»_g(h) = g$(_math(:âˆ˜))h``,
which simplifies for an [`AbstractMultiplicationGroupOperation`](@ref) to ``DÎ»_g(h)[X] = gX``.
"""

@doc "$(_doc_diff_left_compose_mult)"
diff_left_compose(::LieGroup{ğ”½,<:AbstractMultiplicationGroupOperation}, g, h, X) where {ğ”½}

@doc "$(_doc_diff_left_compose_mult)"
function diff_left_compose!(
    G::LieGroup{ğ”½,<:AbstractMultiplicationGroupOperation}, Y, g, h, X
) where {ğ”½}
    return copyto!(LieAlgebra(G), Y, g * X)
end

_doc_diff_right_compose_mult = """
    diff_right_compose(G::LieGroup{ğ”½,<:AbstractMultiplicationGroupOperation}, h, g, X)
    diff_right_compose!(G::LieGroup{ğ”½,<:AbstractMultiplicationGroupOperation}, Y, h, g, X)

Compute the differential of the right group multiplication ``Ï_g(h) = h$(_math(:âˆ˜))g``,
which simplifies for an [`AbstractMultiplicationGroupOperation`](@ref) to ``DÏ_g(h)[X] = Xg``.
"""

@doc "$(_doc_diff_right_compose_mult)"
diff_right_compose(G::LieGroup{ğ”½,<:AbstractMultiplicationGroupOperation}, h, g, X) where {ğ”½}

@doc "$(_doc_diff_right_compose_mult)"
function diff_right_compose!(
    G::LieGroup{ğ”½,<:AbstractMultiplicationGroupOperation}, Y, g, h, X
) where {ğ”½}
    return copyto!(LieAlgebra(G), Y, X * g)
end

_doc_exp_mult = """
    exp(G::LieGroup{ğ”½,MatrixMultiplicationGroupOperation}, e::Identity{MatrixMultiplicationGroupOperation}, X, t::Number=1)
    exp!(G::LieGroup{ğ”½,MatrixMultiplicationGroupOperation}, g, e::Identity{MatrixMultiplicationGroupOperation}, X, t::Number=1)

Compute the Lie group exponential on a [`LieGroup`](@ref) with a [`MatrixMultiplicationGroupOperation`](@ref),
which simplifies to the [matrix exponential](https://en.wikipedia.org/wiki/Matrix_exponential).

This can be computed in-place of `g`.
"""

@doc "$(_doc_exp_mult)"
Base.exp(
    ::LieGroup{ğ”½,MatrixMultiplicationGroupOperation},
    ::Identity{MatrixMultiplicationGroupOperation},
    X,
    t::Number=1,
) where {ğ”½} = exp(t * X)

@doc "$(_doc_exp_mult)"
function ManifoldsBase.exp!(
    ::LieGroup{ğ”½,MatrixMultiplicationGroupOperation},
    g,
    ::Identity{MatrixMultiplicationGroupOperation},
    X,
    t::Number=1,
) where {ğ”½}
    copyto!(g, exp(t .* X))
    return g
end

_doc_identity_element_mat_mult = """
    identity_element(G::LieGroup{ğ”½,MatrixMultiplicationGroupOperation})
    identity_element!(G::LieGroup{ğ”½,MatrixMultiplicationGroupOperation}, e)

Return the a point representation of the [`Identity`](@ref),
which for an [`MatrixMultiplicationGroupOperation`](@ref) is the one-element or identity array.
"""

@doc "$(_doc_identity_element_mat_mult)"
identity_element(::LieGroup{ğ”½,MatrixMultiplicationGroupOperation}) where {ğ”½}

@doc "$(_doc_identity_element_mat_mult)"
identity_element!(::LieGroup{ğ”½,MatrixMultiplicationGroupOperation}, e) where {ğ”½}
function identity_element!(
    ::LieGroup{ğ”½,MatrixMultiplicationGroupOperation}, e::AbstractMatrix
) where {ğ”½}
################buggy -> can create identity_elements of wrong dimensions
    return copyto!(e, LinearAlgebra.I)
end


_doc_identity_element_scalar_mult = """
    identity_element(G::LieGroup{ğ”½,ScalarMultiplicationGroupOperation})
    identity_element!(G::LieGroup{ğ”½,ScalarMultiplicationGroupOperation}, e)

Return the a point representation of the [`Identity`](@ref),
which for an [`ScalarMultiplicationGroupOperation`](@ref) is the one-element.
"""

@doc "$(_doc_identity_element_scalar_mult)"
identity_element(::LieGroup{ğ”½,ScalarMultiplicationGroupOperation}) where {ğ”½}

@doc "$(_doc_identity_element_scalar_mult)"
identity_element!(::LieGroup{ğ”½,ScalarMultiplicationGroupOperation}, e) where {ğ”½}
function identity_element!(
    ::LieGroup{ğ”½, ScalarMultiplicationGroupOperation}, e
) where {ğ”½}
    return copyto!(e , Complex(0,1))
end



_doc_inv_mult = """
    inv(G::LieGroup{ğ”½,<:AbstractMultiplicationGroupOperationroupOperation}, g)
    inv!(G::LieGroup{ğ”½,<:AbstractMultiplicationGroupOperation}, h, g)

Compute the inverse group element ``g^{-1}``, which for an [`AbstractMultiplicationGroupOperation`](@ref)
simplifies to the multiplicative inverse ``g^{-1}``. This can be done in-place of `h`.
"""

@doc "$(_doc_inv_mult)"
Base.inv(G::LieGroup{ğ”½,<:AbstractMultiplicationGroupOperation}, g) where {ğ”½}

@doc "$(_doc_inv_mult)"
function inv!(::LieGroup{ğ”½,<:AbstractMultiplicationGroupOperation}, h, g) where {ğ”½}
    copyto!(h, inv(g))
    return h
end
function inv!(
    G::LieGroup{ğ”½,O}, q, ::Identity{O}
) where {ğ”½,O<:AbstractMultiplicationGroupOperation}
    return identity_element!(G, q)
end

# Compute g^{-1}h more efficient than inverting g
function inv_left_compose!(
    ::LieGroup{ğ”½,MatrixMultiplicationGroupOperation}, k, g, h
) where {ğ”½}
    return copyto!(k, g \ h)
end
# Compute gâˆ˜h^{-1} more efficient than inverting h
function inv_right_compose!(
    ::LieGroup{ğ”½,MatrixMultiplicationGroupOperation}, k, g, h
) where {ğ”½}
    return copyto!(k, g / h)
end

_doc_lie_bracket_mult = """
    lie_bracket(::LieGroup{ğ”½,MatrixMultiplicationGroupOperation}, X, Y)
    lie_bracket!(::LieGroup{ğ”½,MatrixMultiplicationGroupOperation}, Z, X, Y)

Compute the Lie bracket ``[â‹…,â‹…]: $(_math(:ğ”¤))Ã—$(_math(:ğ”¤)) â†’ $(_math(:ğ”¤))``,
which for the for the [`MatrixMultiplicationGroupOperation`](@ref) yields the
commutator bracket

```math
[X, Y] = XY-YX
```

The computation can be done in-place of `Z`.
"""

@doc "$(_doc_lie_bracket_mult)"
lie_bracket(::LieAlgebra{ğ”½,MatrixMultiplicationGroupOperation}, X, Y) where {ğ”½}

@doc "$(_doc_lie_bracket_mult)"
function lie_bracket!(::LieAlgebra{ğ”½,MatrixMultiplicationGroupOperation}, Z, X, Y) where {ğ”½}
    mul!(Z, X, Y)
    mul!(Z, Y, X, -1, true)
    return Z
end

_doc_log_mult = """
    log(G::LieGroup{ğ”½,MatrixMultiplicationGroupOperation}, e::Identity{MatrixMultiplicationGroupOperation}, g)
    log!(G::LieGroup{ğ”½,MatrixMultiplicationGroupOperation}, X, e::Identity{MatrixMultiplicationGroupOperation}, g)

Compute the Lie group logarithm on a [`LieGroup`](@ref) with a [`MatrixMultiplicationGroupOperation`](@ref),
which simplifies to the [matrix logarithm](https://en.wikipedia.org/wiki/Logarithm_of_a_matrix).

This can be computed in-place of `X`.
"""

@doc "$(_doc_log_mult)"
Base.log(
    ::LieGroup{ğ”½,MatrixMultiplicationGroupOperation},
    ::Identity{MatrixMultiplicationGroupOperation},
    g,
) where {ğ”½} = log(g)
function Base.log(
    G::LieGroup{ğ”½,MatrixMultiplicationGroupOperation},
    e::Identity{MatrixMultiplicationGroupOperation},
    ::Identity{MatrixMultiplicationGroupOperation},
) where {ğ”½}
    return zero_vector(G, e)
end

@doc "$(_doc_log_mult)"
function ManifoldsBase.log!(
    ::LieGroup{ğ”½,MatrixMultiplicationGroupOperation},
    X,
    ::Identity{MatrixMultiplicationGroupOperation},
    g,
) where {ğ”½}
    copyto!(X, log(g))
    return X
end

LinearAlgebra.mul!(q, ::Identity{<:AbstractMultiplicationGroupOperation}, p) = copyto!(q, p)
function LinearAlgebra.mul!(
    q::AbstractMatrix, p::AbstractMatrix, ::Identity{MatrixMultiplicationGroupOperation}
)
    return copyto!(q, p)
end
function LinearAlgebra.mul!(
    q::Union{AbstractMatrix},
    ::Identity{<:AbstractMultiplicationGroupOperation},
    ::Identity{<:AbstractMultiplicationGroupOperation},
)
    return copyto!(q, I)
end
function LinearAlgebra.mul!(
    q::Identity{<:AbstractMultiplicationGroupOperation},
    ::Identity{<:AbstractMultiplicationGroupOperation},
    ::Identity{<:AbstractMultiplicationGroupOperation},
)
    return q
end
Base.one(e::Identity{<:AbstractMultiplicationGroupOperation}) = e

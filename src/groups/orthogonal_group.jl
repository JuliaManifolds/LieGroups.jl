"""
    OrthogonalGroup{T}

The orthogonal group ``$(_math(:O))(n)`` is the Lie group consisting of the [`MatrixMultiplicationGroupOperation`](@ref) on the
manifold of rotations [`OrthogonalMatrices`](@extref `Manifolds.OrthogonalMatrices`).

# Constructor
    OrthogonalGroup(n; kwargs...)

Generate  orthogonal group ``$(_math(:O))(n)``.
All keyword arguments in `kwargs...` are passed on to [`OrthogonalMatrices`](@extref `Manifolds.OrthogonalMatrices`) as well.
"""
const OrthogonalGroup{T} = LieGroup{
    ManifoldsBase.‚Ñù,MatrixMultiplicationGroupOperation,Manifolds.OrthogonalMatrices{T}
}

function OrthogonalGroup(n; kwargs...)
    R = Manifolds.OrthogonalMatrices(n; kwargs...)
    return OrthogonalGroup{typeof(R).parameters[1]}(R, MatrixMultiplicationGroupOperation())
end

#
#
# Generic special cases for O(n) and SO(n)

_doc_exp_O2_id = """
    exp(G::OrthogonalGroup{TypeParameter{Tuple{2}}}, ::Identity{MatrixMultiplicationGroupOperation}, X)
    exp(G::SpecialOrthogonalGroup{TypeParameter{Tuple{2}}}, ::Identity{MatrixMultiplicationGroupOperation}, X)
    exp!(G::OrthogonalGroup{TypeParameter{Tuple{2}}}, ::Identity{MatrixMultiplicationGroupOperation}, g, X)
    exp!(G::SpecialOrthogonalGroup{TypeParameter{Tuple{2}}}, ::Identity{MatrixMultiplicationGroupOperation}, g, X)

Compute the Lie group exponential function on the [`OrthogonalGroup`](@ref) ``$(_math(:O))(2)`` or [`SpecialOrthogonalGroup`](@ref) ``$(_math(:SO))(2)``.

Since the Lie algebra of both groups agrees and consist of the set of skew symmetric matrices,
these simplify for the case of ``2√ó2`` matrices to ``X=$(_tex(:pmatrix, "0 & -Œ±", "Œ± & 0"))``, for some ``Œ±‚àà‚Ñù``.

Their exponential is

```math
$(_tex(:exp))_{$(_math(:G))}(X) =  $(_tex(:pmatrix, "$(_tex(:cos))(Œ±) & -$(_tex(:sin))(Œ±)", "$(_tex(:sin))(Œ±) & $(_tex(:cos))(Œ±)")).
```

This result can be computed in-place of `g`.

Note that since ``$(_math(:SO))(2)`` consists of two disjoint connected components and the exponential map is smooth,
the result ``g`` always lies in the connected component of the identity.
"""

@doc "$(_doc_exp_O2_id)"
ManifoldsBase.exp(
    ::OrthogonalGroup{ManifoldsBase.TypeParameter{Tuple{2}}},
    ::Identity{MatrixMultiplicationGroupOperation},
    X,
)
@doc "$(_doc_exp_O2_id)"
ManifoldsBase.exp!(
    ::OrthogonalGroup{ManifoldsBase.TypeParameter{Tuple{2}}},
    g,
    ::Identity{MatrixMultiplicationGroupOperation},
    X,
)

_doc_exp_O3_id = """
    exp(G::OrthogonalGroup{TypeParameter{Tuple{3}}}, ::Identity{MatrixMultiplicationGroupOperation}, X)
    exp(G::SpecialOrthogonalGroup{TypeParameter{Tuple{3}}}, ::Identity{MatrixMultiplicationGroupOperation}, X)
    exp!(G::OrthogonalGroup{TypeParameter{Tuple{3}}}, ::Identity{MatrixMultiplicationGroupOperation}, g, X)
    exp!(G::SpecialOrthogonalGroup{TypeParameter{Tuple{3}}}, ::Identity{MatrixMultiplicationGroupOperation}, g, X)

Compute the Lie group exponential function on the [`OrthogonalGroup`](@ref) ``$(_math(:O))(3)`` or [`SpecialOrthogonalGroup`](@ref) ``$(_math(:SO))(3)``.

Since the Lie algebra of both groups agrees and consist of the set of skew symmetric matrices,
the ``3√ó3`` skew symmetric matrices are of the form

```math
    X = $(_tex(:pmatrix, "0 & -c & b", "c & 0 & -a", "-b & a & 0")),
```
for some ``a, b, c ‚àà ‚Ñù``. To compute the exponential, the [Rodrigues' rotation formula](https://en.wikipedia.org/wiki/Olinde_Rodrigues)
can be used. With ``Œ± = $(_tex(:sqrt, "a^2+b^2+c^2")) = $(_tex(:frac, "1", _tex(:sqrt,"2")))$(_tex(:norm,"X"))``
we obtain for ``Œ± ‚â† 0``

```math
$(_tex(:exp))_{$(_math(:G))}(X) = I_3 + $(_tex(:frac, _tex(:sin, "Œ±"), "Œ±"))X + $(_tex(:frac, "(1 - $(_tex(:cos, "Œ±")))", "Œ±^2"))X^2,
```

and $(_tex(:exp))_{$(_math(:G))}(X) = I_3`` otherwise.

This result can be computed in-place of `g`.

Note that since ``$(_math(:SO))(3)`` consists of two disjoint connected components and the exponential map is smooth,
the result ``g`` always lies in the connected component of the identity.
"""

@doc "$(_doc_exp_O3_id)"
ManifoldsBase.exp(
    ::OrthogonalGroup{ManifoldsBase.TypeParameter{Tuple{3}}},
    ::Identity{MatrixMultiplicationGroupOperation},
    X,
)
@doc "$(_doc_exp_O3_id)"
ManifoldsBase.exp!(
    ::OrthogonalGroup{ManifoldsBase.TypeParameter{Tuple{3}}},
    g,
    ::Identity{MatrixMultiplicationGroupOperation},
    X,
)

_doc_exp_O4_id = """
    exp(G::OrthogonalGroup{TypeParameter{Tuple{4}}}, ::Identity{MatrixMultiplicationGroupOperation}, X)
    exp(G::SpecialOrthogonalGroup{TypeParameter{Tuple{4}}}, ::Identity{MatrixMultiplicationGroupOperation}, X)
    exp!(G::OrthogonalGroup{TypeParameter{Tuple{4}}}, ::Identity{MatrixMultiplicationGroupOperation}, g, X)
    exp!(G::SpecialOrthogonalGroup{TypeParameter{Tuple{4}}}, ::Identity{MatrixMultiplicationGroupOperation}, g, X)

Compute the Lie group exponential function on the [`OrthogonalGroup`](@ref) ``$(_math(:O))(4)`` or [`SpecialOrthogonalGroup`](@ref) ``$(_math(:SO))(4)``.

Similar to the ``3√ó3`` case, an efficient computation is provided,
adapted from [GallierXu:2002](@cite), [AndricaRohan:2013](@cite) with a few numerical stabilisations.
"""

@doc "$(_doc_exp_O4_id)"
ManifoldsBase.exp(
    ::OrthogonalGroup{ManifoldsBase.TypeParameter{Tuple{4}}},
    ::Identity{MatrixMultiplicationGroupOperation},
    X,
)
@doc "$(_doc_exp_O4_id)"
ManifoldsBase.exp!(
    ::OrthogonalGroup{ManifoldsBase.TypeParameter{Tuple{4}}},
    g,
    ::Identity{MatrixMultiplicationGroupOperation},
    X,
)

function ManifoldsBase.exp(
    G::CommonUnitarySubGroups{ManifoldsBase.‚Ñù,ManifoldsBase.TypeParameter{Tuple{2}}},
    e::Identity{MatrixMultiplicationGroupOperation},
    X,
)
    g = ManifoldsBase.allocate_result(G, exp, X)
    exp!(G, g, e, X)
    return g
end

function ManifoldsBase.exp!(
    ::CommonUnitarySubGroups{‚Ñù,ManifoldsBase.TypeParameter{Tuple{2}}},
    g,
    ::Identity{MatrixMultiplicationGroupOperation},
    X,
)
    @assert size(X) == (2, 2)
    @assert size(g) == (2, 2)
    @inbounds Œ± = (X[2, 1] - X[1, 2]) / 2
    sinŒ±, cosŒ± = sincos(Œ±)
    @inbounds begin
        g[1, 1] = cosŒ±
        g[2, 1] = sinŒ±
        g[1, 2] = -sinŒ±
        g[2, 2] = cosŒ±
    end
    return g
end

function ManifoldsBase.exp!(
    ::CommonUnitarySubGroups{‚Ñù,ManifoldsBase.TypeParameter{Tuple{3}}},
    g,
    ::Identity{MatrixMultiplicationGroupOperation},
    X,
)
    Œ∏ = norm(X) / sqrt(2)
    if Œ∏ ‚âà 0
        a = 1 - Œ∏^2 / 6
        b = Œ∏ / 2
    else
        a = sin(Œ∏) / Œ∏
        b = (1 - cos(Œ∏)) / Œ∏^2
    end
    copyto!(g, LinearAlgebra.I)
    g .+= a .* X
    mul!(g, X, X, b, true)
    return g
end
function ManifoldsBase.exp!(
    ::CommonUnitarySubGroups{‚Ñù,ManifoldsBase.TypeParameter{Tuple{4}}},
    g,
    ::Identity{MatrixMultiplicationGroupOperation},
    X,
)
    T = eltype(X)
    Œ±, Œ≤ = angles_4d_skew_sym_matrix(X)
    sinŒ±, cosŒ± = sincos(Œ±)
    sinŒ≤, cosŒ≤ = sincos(Œ≤)
    Œ±¬≤ = Œ±^2
    Œ≤¬≤ = Œ≤^2
    Œî = Œ≤¬≤ - Œ±¬≤
    if !isapprox(Œî, 0; atol=1e-6)  # Case Œ± > Œ≤ ‚â• 0
        sincŒ± = sinŒ± / Œ±
        sincŒ≤ = Œ≤ == 0 ? one(T) : sinŒ≤ / Œ≤
        a‚ÇÄ = (Œ≤¬≤ * cosŒ± - Œ±¬≤ * cosŒ≤) / Œî
        a‚ÇÅ = (Œ≤¬≤ * sincŒ± - Œ±¬≤ * sincŒ≤) / Œî
        a‚ÇÇ = (cosŒ± - cosŒ≤) / Œî
        a‚ÇÉ = (sincŒ± - sincŒ≤) / Œî
    elseif Œ± == 0 # Case Œ± = Œ≤ = 0
        a‚ÇÄ = a‚ÇÅ = one(T)
        a‚ÇÇ = inv(T(2))
        a‚ÇÉ = inv(T(6))
    else  # Case Œ± ‚™Ü Œ≤ ‚â• 0, Œ± ‚â† 0
        sincŒ± = sinŒ± / Œ±
        r = Œ≤ / Œ±
        c = 1 / (1 + r)
        d = Œ± * (Œ± - Œ≤) / 2
        if Œ± < 1e-2
            e = evalpoly(Œ±¬≤, (inv(T(3)), inv(T(-30)), inv(T(840)), inv(T(-45360))))
        else
            e = (sincŒ± - cosŒ±) / Œ±¬≤
        end
        a‚ÇÄ = (Œ± * sinŒ± + (1 + r - d) * cosŒ±) * c
        a‚ÇÅ = ((3 - d) * sincŒ± - (2 - r) * cosŒ±) * c
        a‚ÇÇ = (sincŒ± - (1 - r) / 2 * cosŒ±) * c
        a‚ÇÉ = (e + (1 - r) * (e - sincŒ± / 2)) * c
    end
    X¬≤ = X * X
    X¬≥ = X¬≤ * X
    g .= a‚ÇÄ * LinearAlgebra.I + a‚ÇÅ .* X .+ a‚ÇÇ .* X¬≤ .+ a‚ÇÉ .* X¬≥
    return g
end

_doc_get_coordinates_On = """
    get_coordinates(G::OrthogonalGroup, e, X, ::LieAlgebraOrthogonalBasis)
    get_coordinates(G::SpecialOrthogonalGroup, e, X, ::LieAlgebraOrthogonalBasis)
    get_coordinates!(G::OrthogonalGroup, c, e, X ::LieAlgebraOrthogonalBasis)
    get_coordinates!(G::SpecialOrthogonalGroup, c, e, X ::LieAlgebraOrthogonalBasis)

Compute the vector of coordinates ``c ‚àà ‚Ñù^d`` from the Lie algebra tangent vector ``X ‚àà ùî∞ùî¨(n)``
of the [`OrthogonalGroup`](@ref) `O(n)` in the [`LieAlgebraOrthogonalBasis`](@ref),
where ``d`` is the dimension of the Lie algebra. This is also the version used in [`vee`](@ref).

For ``O(2)`` there is only one coefficient ``Œ±`` in the basis ``$(_tex(:pmatrix, "0 & -Œ±", "Œ± & 0"))``,
which is returned as ``c = (Œ±)^$(_tex(:transp))``.

A usual basis representation of ``ùî∞ùî¨(3) is given by
```math
    X = $(_tex(:pmatrix, "0 & -Œ≥ & Œ≤", "Œ≥ & 0 & -Œ±", "-Œ≤ & Œ± & 0")),
```
hence the coordinate vector is ``c = (Œ±, Œ≤, Œ≥)^$(_tex(:transp)) ‚àà ‚Ñù^3``.

For `n ‚â• 4`` the lower triangular part is added to ``c`` row-wise.
"""

@doc "$(_doc_get_coordinates_On)"
get_coordinates(G::OrthogonalGroup, e, X, ::LieAlgebraOrthogonalBasis)

@doc "$(_doc_get_coordinates_On)"
get_coordinates!(G::OrthogonalGroup, c, e, X, ::LieAlgebraOrthogonalBasis)

function get_coordinates_lie!(
    G::CommonUnitarySubGroups{‚Ñù,ManifoldsBase.TypeParameter{Tuple{2}}},
    c,
    e::Identity{MatrixMultiplicationGroupOperation},
    X,
)
    @assert size(X) == (2, 2)
    @assert size(c) == (1,)
    c[1] = X[2, 1]
    return c
end
function get_coordinates_lie!(
    ::CommonUnitarySubGroups{‚Ñù,ManifoldsBase.TypeParameter{Tuple{n}}},
    c,
    ::Identity{MatrixMultiplicationGroupOperation},
    X,
) where {n}
    @assert size(X) == (n, n)
    @assert length(c) == manifold_dimension(G)
    @assert n > 2
    _get_coordinates_lie_On!(c, X)
    return c
end
function get_coordinates_lie!(
    G::CommonUnitarySubGroups{‚Ñù}, c, ::Identity{MatrixMultiplicationGroupOperation}, X
)
    n = get_parameter(G.manifold.size)[1]
    @assert length(c) == manifold_dimension(G)
    @assert size(X) == (n, n)
    if n == 2
        c[1] = X[2, 1]
    else
        _get_coordinates_lie_On!(c, X)
    end
    return c
end

function _get_coordinates_lie_On!(c, X)
    n = size(X, 1)
    @inbounds begin
        c[1] = X[3, 2]
        c[2] = X[1, 3]
        c[3] = X[2, 1]
        k = 4
        for i in 4:n, j in 1:(i - 1)
            c[k] = X[i, j]
            k += 1
        end
    end
    return c
end

_doc_get_vector_On = """
    get_vector(G::OrthogonalGroup, e, c, ::LieAlgebraOrthogonalBasis)
    get_vector(G::SpecialOrthogonalGroup, e, c, ::LieAlgebraOrthogonalBasis)
    get_vector!(G::OrthogonalGroup, X, e, c, ::LieAlgebraOrthogonalBasis)
    get_vector!(G::SpecialOrthogonalGroup, X, e, c, ::LieAlgebraOrthogonalBasis)

Compute the tangent vector ``X ‚àà ùî∞ùî¨(n)`` based on a vector of coordinates ``c ‚àà ‚Ñù^d``,
where ``d`` is the dimension of the Lie algebra of the [`OrthogonalGroup`](@ref) `O(n)`
and the coordinates are with respect to the [`LieAlgebraOrthogonalBasis`](@ref).
This is also the version used in [`hat`](@ref).

For ``O(2)`` there is only one coefficient ````c = (Œ±)^$(_tex(:transp))`` and hence
``X = $(_tex(:pmatrix, "0 & -Œ±", "Œ± & 0"))`` is returned.

For ``n=3`` a usual representtion turns ``c = (Œ±, Œ≤, Œ≥)^$(_tex(:transp)) ‚àà ‚Ñù^3`` into
```math
    X = $(_tex(:pmatrix, "0 & -Œ≥ & Œ≤", "Œ≥ & 0 & -Œ±", "-Œ≤ & Œ± & 0")),
```
hence the coordinate vector is .

For `n ‚â• 4`` all further coefficients are used to fill up the following rows of the lower
triangular part ‚Äì which determines the upper triangualr part due to skew-symmetry
"""

@doc "$(_doc_get_vector_On)"
get_vector(G::OrthogonalGroup, e, X, ::LieAlgebraOrthogonalBasis)

@doc "$(_doc_get_vector_On)"
get_vector!(G::OrthogonalGroup, c, e, X::LieAlgebraOrthogonalBasis)

function get_vector_lie!(
    G::CommonUnitarySubGroups{‚Ñù,ManifoldsBase.TypeParameter{Tuple{2}}},
    X,
    e::Identity{MatrixMultiplicationGroupOperation},
    c,
)
    @assert size(X) == (2, 2)
    @assert size(c) == (1,)
    X[1, 1] = 0.0
    X[2, 1] = c[1]
    X[1, 2] = -c[1]
    X[2, 2] = 0.0
    return X
end
function get_vector_lie!(
    ::CommonUnitarySubGroups{‚Ñù,ManifoldsBase.TypeParameter{Tuple{n}}},
    X,
    ::Identity{MatrixMultiplicationGroupOperation},
    c,
) where {n}
    @assert size(X) == (n, n)
    @assert length(c) == manifold_dimension(G)
    @assert n > 2
    _get_vector_lie_On!(X, c)
    return X
end
function get_vector_lie!(
    G::CommonUnitarySubGroups{‚Ñù}, X, ::Identity{MatrixMultiplicationGroupOperation}, c
)
    n = get_parameter(G.manifold.size)[1]
    @assert length(c) == manifold_dimension(G)
    @assert size(X) == (n, n)
    if n == 2
        X[1, 1] = 0.0
        X[2, 1] = c[1]
        X[1, 2] = -c[1]
        X[2, 2] = 0.0
    else
        _get_vector_lie_On!(X, c)
    end
    return X
end

function _get_vector_lie_On!(X, c)
    n = size(X, 1)
    @inbounds begin
        X[1, 2] = c[3]
        X[2, 1] = -c[3]
        X[1, 3] = c[2]
        X[3, 1] = -c[2]
        X[2, 3] = -c[1]
        X[3, 2] = c[1]
        k = 4
        for i in 4:n
            X[i, i] = 0.0
            for j in 1:(i - 1)
                X[i, j] = c[k]
                X[j, i] = -c[k]
                k += 1
            end
        end
    end
    return X
end

_doc_log_O2_id = """
    log(G::OrthogonalGroup{TypeParameter{Tuple{2}}}, ::Identity{MatrixMultiplicationGroupOperation}, g)
    log(G::SpecialOrthogonalGroup{TypeParameter{Tuple{2}}}, ::Identity{MatrixMultiplicationGroupOperation}, g)
    log!(G::OrthogonalGroup{TypeParameter{Tuple{2}}}, X, ::Identity{MatrixMultiplicationGroupOperation}, g)
    log!(G::SpecialOrthogonalGroup{TypeParameter{Tuple{2}}}, X, ::Identity{MatrixMultiplicationGroupOperation}, g)

Compute the Lie group logarithm function on the [`OrthogonalGroup`](@ref) ``$(_math(:O))(2)`` or [`SpecialOrthogonalGroup`](@ref) ``$(_math(:SO))(2)``.

For the two-dimensional case, any rotation matrix ``g`` can be represented as ``$(_tex(:pmatrix, "$(_tex(:cos))(Œ±) & -$(_tex(:sin))(Œ±)", "$(_tex(:sin))(Œ±) & $(_tex(:cos))(Œ±)"))``.
For the [`SpecialOrthogonalGroup`](@ref), ``g`` might also include reflections.

The logarithm is then

```math
$(_tex(:log))_{$(_math(:G))}(g) =  $(_tex(:pmatrix, "0 & Œ± &", "-Œ± & 0")).
```

This result can be computed in-place of `X`

Note the logarithmic map is only locally around the identity uniquely determined.
Especially, since ``$(_math(:SO))(2)`` consists of two disjoint connected components and the exponential map is smooth,
for any ``g`` in the other component, the logarithmic map is defined, but not the inverse of the exponential map.
"""

@doc "$(_doc_log_O2_id)"
ManifoldsBase.log(
    ::OrthogonalGroup{ManifoldsBase.TypeParameter{Tuple{2}}},
    ::Identity{MatrixMultiplicationGroupOperation},
    g,
)

@doc "$(_doc_log_O2_id)"
ManifoldsBase.log!(
    ::OrthogonalGroup{ManifoldsBase.TypeParameter{Tuple{2}}},
    X,
    ::Identity{MatrixMultiplicationGroupOperation},
    g,
)

_doc_log_O3_id = """
    log(G::OrthogonalGroup{TypeParameter{Tuple{3}}}, ::Identity{MatrixMultiplicationGroupOperation}, g)
    log(G::SpecialOrthogonalGroup{TypeParameter{Tuple{3}}}, ::Identity{MatrixMultiplicationGroupOperation}, g)
    log!(G::OrthogonalGroup{TypeParameter{Tuple{3}}}, X, ::Identity{MatrixMultiplicationGroupOperation}, g)
    log!(G::SpecialOrthogonalGroup{TypeParameter{Tuple{3}}}, X, ::Identity{MatrixMultiplicationGroupOperation}, g)

Compute the Lie group logarithm function on the [`OrthogonalGroup`](@ref) ``$(_math(:O))(3)`` or [`SpecialOrthogonalGroup`](@ref) ``$(_math(:SO))(3)``.
``$(_tex(:exp))_{$(_math(:G))}(X) = g`` is to invert the [Rodrigues' rotation formula](https://en.wikipedia.org/wiki/Olinde_Rodrigues)

```math
$(_tex(:exp))_{$(_math(:G))}(X) = I_3 + $(_tex(:frac, _tex(:sin, "Œ±"), "Œ±"))X + $(_tex(:frac, "(1 - $(_tex(:cos, "Œ±")))", "Œ±^2"))X^2,
```

For ``Œ± ‚àâ $(_tex(:Set, "0, œÄ"))`` we obtain ``X`` from the observation that
```math
$(_tex(:rm, "tr"))(g) = 1 + 2$(_tex(:cos))(Œ±)
$(_tex(:qquad))$(_tex(:text, " and "))$(_tex(:qquad))
$(_tex(:frac, "1", "2"))(g-g^$(_tex(:transp))) = $(_tex(:sin))(Œ±)X.
```

For ``Œ± = 0`` we have ``g = I_3`` and ``X = 0``.

For ``Œ± = œÄ`` we have to solve ``X^2 = $(_tex(:frac, "1", "2"))(g-I_3)``,
where ``X`` is skew-symmetric and hence we have to solve for three unknowns.

```math
$(_tex(:log))_{$(_math(:G))}(g) = X.
```

This result can be computed in-place of `X`

Note the logarithmic map is only locally around the identity uniquely determined.
Especially, since ``$(_math(:SO))(3)`` consists of two disjoint connected components and the exponential map is smooth,
for any ``g`` in the other component, the logarithmic map is defined, but not the inverse of the exponential map.

"""

@doc "$(_doc_log_O3_id)"
ManifoldsBase.log(
    ::OrthogonalGroup{ManifoldsBase.TypeParameter{Tuple{3}}},
    ::Identity{MatrixMultiplicationGroupOperation},
    g,
)

@doc "$(_doc_log_O3_id)"
ManifoldsBase.log!(
    ::OrthogonalGroup{ManifoldsBase.TypeParameter{Tuple{3}}},
    X,
    ::Identity{MatrixMultiplicationGroupOperation},
    g,
)

_doc_log_O4_id = """
    log(G::OrthogonalGroup{TypeParameter{Tuple{4}}}, ::Identity{MatrixMultiplicationGroupOperation}, g)
    log(G::SpecialOrthogonalGroup{TypeParameter{Tuple{4}}}, ::Identity{MatrixMultiplicationGroupOperation}, g)
    log!(G::OrthogonalGroup{TypeParameter{Tuple{4}}}, X, ::Identity{MatrixMultiplicationGroupOperation}, g)
    log!(G::SpecialOrthogonalGroup{TypeParameter{Tuple{4}}}, X, ::Identity{MatrixMultiplicationGroupOperation}, g)

Compute the Lie group logarithm function on the [`OrthogonalGroup`](@ref) ``$(_math(:O))(4)`` or [`SpecialOrthogonalGroup`](@ref) ``$(_math(:SO))(4)``.

The implementation is based on a generalized variant of the Rodrigues' like formula.
For details, see [GallierXu:2002; Section 3](@cite).

This result can be computed in-place of `X`

Note the logarithmic map is only locally around the identity uniquely determined.
"""

@doc "$(_doc_log_O4_id)"
ManifoldsBase.log(
    ::OrthogonalGroup{ManifoldsBase.TypeParameter{Tuple{4}}},
    ::Identity{MatrixMultiplicationGroupOperation},
    g,
)

@doc "$(_doc_log_O4_id)"
ManifoldsBase.log!(
    ::OrthogonalGroup{ManifoldsBase.TypeParameter{Tuple{4}}},
    X,
    ::Identity{MatrixMultiplicationGroupOperation},
    g,
)

function ManifoldsBase.log(
    G::CommonUnitarySubGroups{
        ManifoldsBase.‚Ñù,<:ManifoldsBase.TypeParameter{<:Union{Tuple{2},Tuple{3},Tuple{4}}}
    },
    e::Identity{MatrixMultiplicationGroupOperation},
    g,
)
    Y = ManifoldsBase.allocate_result(G, log, g)
    log!(G, Y, e, g)
    return Y
end
# Resolve an ambiguity compared to the general matrix multiplication definitions
function Base.log(
    G::CommonUnitarySubGroups{
        ManifoldsBase.‚Ñù,<:ManifoldsBase.TypeParameter{<:Union{Tuple{2},Tuple{3},Tuple{4}}}
    },
    e::Identity{MatrixMultiplicationGroupOperation},
    ::Identity{MatrixMultiplicationGroupOperation},
)
    return zero_vector(G, e)
end

function ManifoldsBase.log!(
    ::CommonUnitarySubGroups{ManifoldsBase.‚Ñù,ManifoldsBase.TypeParameter{Tuple{2}}},
    X,
    ::Identity{MatrixMultiplicationGroupOperation},
    g,
)
    @assert size(X) == (2, 2)
    @assert size(g) == (2, 2)
    @inbounds Œ± = atan(g[2, 1], g[1, 1])
    @inbounds begin
        X[1, 1] = 0
        X[2, 1] = Œ±
        X[1, 2] = -Œ±
        X[2, 2] = 0
    end
    return X
end
function ManifoldsBase.log!(
    G::CommonUnitarySubGroups{‚Ñù,ManifoldsBase.TypeParameter{Tuple{3}}},
    X::AbstractMatrix,
    e::Identity{MatrixMultiplicationGroupOperation},
    q::AbstractMatrix,
)
    cosŒ∏ = (tr(q) - 1) / 2
    if cosŒ∏ ‚âà -1
        eig = eigen_safe(q)
        ival = findfirst(Œª -> isapprox(Œª, 1), eig.values)
        inds = SVector{3}(1:3)
        ax = eig.vectors[inds, ival]
        return get_vector!(G, X, e, œÄ * ax, LieAlgebraOrthogonalBasis())
    end
    X .= q ./ usinc_from_cos(cosŒ∏)
    # project onto ùî∞ùî¨(3) for numerical stability
    return project!(G, X, e, X)
end
function ManifoldsBase.log!(
    G::CommonUnitarySubGroups{‚Ñù,ManifoldsBase.TypeParameter{Tuple{4}}},
    X::AbstractMatrix,
    e::Identity{MatrixMultiplicationGroupOperation},
    q::AbstractMatrix,
)
    cosŒ±, cosŒ≤ = cos_angles_4d_rotation_matrix(q)
    Œ± = acos(clamp(cosŒ±, -1, 1))
    Œ≤ = acos(clamp(cosŒ≤, -1, 1))
    if Œ± ‚âà 0 && Œ≤ ‚âà œÄ
        A¬≤ = Symmetric((q - I) ./ 2)
        P = eigvecs(A¬≤)
        E = similar(q)
        fill!(E, 0)
        @inbounds begin
            E[2, 1] = -Œ≤
            E[1, 2] = Œ≤
        end
        copyto!(X, P * E * transpose(P))
    else
        det(q) < 0 && throw(
            DomainError(
                "The Lie group logarithm is not defined for $q with a negative determinant ($(det(q)) < 0). Point `q` is in a different connected component of the manifold $G",
            ),
        )
        copyto!(X, log(q))
    end
    return project!(G, X, e, X)
end

function Base.show(io::IO, G::OrthogonalGroup)
    size = Manifolds.get_parameter(G.manifold.size)[1]
    return print(io, "OrthogonalGroup($(size))")
end

"""
    UnitaryGroup{T}

The special orthogonal group ``$(_math(:U))(n)`` is the Lie group consisting of the [`MatrixMultiplicationGroupOperation`](@ref) on the
manifold of rotations [`UnitaryMatrices`](@extref `Manifolds.GeneralUnitaryMatrices`) with absolute value of the determinant equal to one.

# Constructor
    UnitaryGroup(n::Int, ùîΩ::AbstractNumbers=‚ÑÇ; kwargs...)

Generate unitary group ``$(_math(:U))(n)``.
All keyword arguments in `kwargs...` are passed on to [`Rotations`](@extref `Manifolds.Rotations`) as well.
"""
const UnitaryGroup{ùîΩ,T} = LieGroup{
    ùîΩ,MatrixMultiplicationGroupOperation,Manifolds.UnitaryMatrices{T,ùîΩ}
}

function UnitaryGroup(n::Int, ùîΩ::AbstractNumbers=ManifoldsBase.‚ÑÇ; kwargs...)
    U = Manifolds.UnitaryMatrices(n, ùîΩ; kwargs...)
    return UnitaryGroup{ùîΩ,typeof(U).parameters[1]}(U, MatrixMultiplicationGroupOperation())
end

function ManifoldsBase.check_size(
    ::UnitaryGroup{‚Ñç,ManifoldsBase.TypeParameter{Tuple{1}}}, p::Number
)
    return nothing
end
function ManifoldsBase.check_size(
    ::UnitaryGroup{‚Ñç,ManifoldsBase.TypeParameter{Tuple{1}}}, p, X::Number
)
    return nothing
end
function ManifoldsBase.check_size(
    G::UnitaryGroup{‚Ñç,ManifoldsBase.TypeParameter{Tuple{1}}}, p
)
    if size(p) != ()
        return DomainError(
            size(p),
            "The point $(p) can not belong to the manifold $(G), since its size $(size(p)) is not equal to the manifolds representation size for (mutable) Quaternions (()).",
        )
    end
    return nothing
end
function ManifoldsBase.check_size(
    G::UnitaryGroup{‚Ñç,ManifoldsBase.TypeParameter{Tuple{1}}}, p, X
)
    if size(X) != ()
        return DomainError(
            size(X),
            "The point $(p) can not belong to the manifold $(G), since its size $(size(X)) is not equal to the manifolds representation size for (mutable) Quaternions (()).",
        )
    end
    return nothing
end

function Base.exp(
    ::UnitaryGroup{ManifoldsBase.‚Ñç,ManifoldsBase.TypeParameter{Tuple{1}}}, X::Number
)
    return exp(X)
end
function Base.exp(
    ::UnitaryGroup{ManifoldsBase.‚Ñç,ManifoldsBase.TypeParameter{Tuple{1}}},
    g::Number,
    X::Number,
)
    return g * exp(X)
end

function identity_element(
    ::UnitaryGroup{ManifoldsBase.‚Ñç,ManifoldsBase.TypeParameter{Tuple{1}}}
)
    return Quaternions.quat(1.0)
end

Base.inv(::UnitaryGroup, g) = adjoint(g)
inv!(G::UnitaryGroup, h, g) = copyto!(G, h, adjoint(g))
function inv!(::UnitaryGroup{ManifoldsBase.‚Ñç,ManifoldsBase.TypeParameter{Tuple{1}}}, h, g)
    h[] = adjoint(g[])
    return h
end

function Base.log(
    ::UnitaryGroup{ManifoldsBase.‚Ñç,ManifoldsBase.TypeParameter{Tuple{1}}}, g::Number
)
    return log(g)
end
function Base.log(
    G::UnitaryGroup{ManifoldsBase.‚Ñç,ManifoldsBase.TypeParameter{Tuple{1}}},
    g::Number,
    h::Number,
)
    return log(inv(G, g) * h)
end
function ManifoldsBase.log!(
    ::UnitaryGroup{ManifoldsBase.‚Ñç,ManifoldsBase.TypeParameter{Tuple{1}}}, X, g
)
    X[] = log(g[])
    return X
end
function ManifoldsBase.log!(
    G::UnitaryGroup{ManifoldsBase.‚Ñç,ManifoldsBase.TypeParameter{Tuple{1}}}, X, g, h
)
    X[] = log(inv(G, g) * h)
    return nothing
end
#
#
# A common type for all 4 groups: O, SO, SU, U, because they share quite some implementations
# Stored in this, since this is the most generic case
#
"""
    CommonUnitarySubGroup{ùîΩ,T}

A constant that allows to refer to several subgroups of ``$(_math(:U))(n)`` for
implementations where
* certain subgroups real/complex share a common implementation, e.g. for the same sizes `T` usually via [`TypeParameter`](@extref `ManifoldsBase.TypeParameter`)
* certain functions are the same for all sizes `T` as long as the field `ùîΩ` is the same
"""
const CommonUnitarySubGroup{ùîΩ,T} = LieGroup{
    ùîΩ,MatrixMultiplicationGroupOperation,<:Manifolds.GeneralUnitaryMatrices{T,ùîΩ}
}

#
#
# A common type for all 4 groups: O, SO, SU, U, because they share quite some implementations
# Stored in this, since this is the most generic case
#
"""
    CommonUnitarySubAlgebra{ùîΩ,T}

A constant that allows to refer to several sub Algebras of ``$(_math(:u))(n)`` for
implementations where
* certain sub algebras real/complex share a common implementation, e.g. for the same sizes `T` usually via [`TypeParameter`](@extref `ManifoldsBase.TypeParameter`)
* certain functions are the same for all sizes `T` as long as the field `ùîΩ` is the same
"""
const CommonUnitarySubAlgebra{ùîΩ,T} = LieAlgebra{
    ùîΩ,MatrixMultiplicationGroupOperation,<:CommonUnitarySubGroup{ùîΩ,T}
}

function Base.show(
    io::IO, ::UnitaryGroup{ManifoldsBase.‚ÑÇ,ManifoldsBase.TypeParameter{Tuple{n}}}
) where {n}
    return print(io, "UnitaryGroup($(n))")
end
function Base.show(io::IO, M::UnitaryGroup{ManifoldsBase.‚ÑÇ,Tuple{Int}})
    n = ManifoldsBase.get_parameter(M.manifold.size)[1]
    return print(io, "UnitaryGroup($(n); parameter=:field)")
end
function Base.show(
    io::IO, ::UnitaryGroup{ManifoldsBase.‚Ñç,ManifoldsBase.TypeParameter{Tuple{n}}}
) where {n}
    return print(io, "UnitaryGroup($(n), ‚Ñç)")
end
function Base.show(io::IO, G::UnitaryGroup{ManifoldsBase.‚Ñç,Tuple{Int}})
    n = ManifoldsBase.get_parameter(G.manifold.size)[1]
    return print(io, "UnitaryGroup($(n), ‚Ñç; parameter=:field)")
end

#
#
# Together with a product group operation, that is a tuple of operations,
# One for each factor, we define the product operation as acting element wise.

"""
    AbstractProductGroupOperation <: AbstractGroupOperation

An abstract type to model group operations on a product manifold
"""
abstract type AbstractProductGroupOperation <: AbstractGroupOperation end

"""
    ProductGroupOperation{O<:NTuple{N,AbstractGroupOperation} where N} <: AbstractProductGroupOperation

A struct do model a tuple of group operations, one for each factor of a product group,
that together forms a new group operation.

Access to the single operations can be done by `pgo[i]`.

# Constructor

    ProductGroupOperation(o::AbstractGroupOperation...)
    Ã—(o::AbstractGroupOperation...) = ProductGroupOperation(o...)
"""
struct ProductGroupOperation{OTM <: NTuple{N, AbstractGroupOperation} where {N}} <:
    AbstractProductGroupOperation
    operations::OTM
end
function ProductGroupOperation(operations::AbstractGroupOperation...)
    return ProductGroupOperation(operations)
end

@doc raw"""
    cross(O1::AbstractGroupOperation, O2::AbstractGroupOperation)
    O1 Ã— O2
    O1 Ã— O2 Ã— O3 Ã— ...

Return the [`ProductGroupOperation`](@ref) For two [AbstractGroupOperation`](@ref) `O1` and `O2`,
where for the case that one of them is a [`ProductGroupOperation`](@ref) itself,
the other is either prepended (if `O1` is a product) or appended (if `O2` is).
If both are product operations, they are combined into one, keeping the order of operations.

For the case that more than two are concatenated with `Ã—` this is iterated.
"""
LinearAlgebra.cross(::AbstractGroupOperation...)
function LinearAlgebra.cross(O1::AbstractGroupOperation, O2::AbstractGroupOperation)
    return ProductGroupOperation(O1, O2)
end
function LinearAlgebra.cross(P::ProductGroupOperation, O::AbstractGroupOperation)
    return ProductGroupOperation(P.operations..., O)
end
function LinearAlgebra.cross(O::AbstractGroupOperation, P::ProductGroupOperation)
    return ProductGroupOperation(O, P.operations...)
end
function LinearAlgebra.cross(P1::ProductGroupOperation, P2::ProductGroupOperation)
    return ProductGroupOperation(P1.operations..., P2.operations...)
end

"""
    ProductLieGroup(G1, G2, ..., Gn)

Return the [`LieGroup`](@ref) of the product of Lie groups `G1`, `G2`, up to `Gn`
and all following Lie groups.
This can be considered as a vector of Lie group, where the vector is always
of the same length as the number of provided Lie Groups.

If none of the Lie groups are product Lie groups themselves,
this is equivalent to `G1 Ã— G2 Ã— ... Ã— Gn`.

For an example illustrating the differences see [`x`](@ref LinearAlgebra.cross(::LieGroup...)`(::LieGroup...)`.
"""
function ProductLieGroup(lie_groups::LieGroup...)
    return LieGroup(
        ProductManifold([L.manifold for L in lie_groups]...),
        ProductGroupOperation([L.op for L in lie_groups]...)
    )
end
# Do not â€œwrap twiceâ€
function ProductLieGroup(G::LieGroup{ğ”½, Op, M}) where {ğ”½, Op <: ProductGroupOperation, M <: ProductManifold}
    return G
end


function ManifoldsBase.submanifold_components(
        ::LieGroup{ğ”½, Op, M}, op::ProductGroupOperation
    ) where {ğ”½, Op <: ProductGroupOperation, M <: ProductManifold}
    return op.operations
end
function ManifoldsBase.submanifold_components(
        PrG::LieGroup{ğ”½, Op, M}, ::Identity{Op}
    ) where {ğ”½, Op <: ProductGroupOperation, M <: ProductManifold}
    return map(Identity, PrG.op.operations)
end

function _compose!(
        PrG::LieGroup{ğ”½, Op, M}, k, g, h
    ) where {ğ”½, Op <: ProductGroupOperation, M <: ProductManifold}
    map(
        compose!,
        map(LieGroup, PrG.manifold.manifolds, PrG.op.operations),
        submanifold_components(PrG.manifold, k),
        submanifold_components(PrG.manifold, g),
        submanifold_components(PrG.manifold, h),
    )
    return k
end

function ManifoldsBase.check_size(
        PrG::LieGroup{ğ”½, Op, M}, g
    ) where {ğ”½, Op <: AbstractProductGroupOperation, M <: ProductManifold}
    return ManifoldsBase.check_size(PrG.manifold, g)
end
function ManifoldsBase.check_size(
        ::LieGroup{ğ”½, Op, M}, ::Identity
    ) where {ğ”½, Op <: AbstractProductGroupOperation, M <: ProductManifold}
    return nothing
end
function ManifoldsBase.check_size(
        PrG::LieGroup{ğ”½, Op, M}, g, X
    ) where {ğ”½, Op <: AbstractProductGroupOperation, M <: ProductManifold}
    return ManifoldsBase.check_size(PrG.manifold, g, X)
end
function ManifoldsBase.check_size(
        PrG::LieGroup{ğ”½, Op, M}, ::Identity, X
    ) where {ğ”½, Op <: AbstractProductGroupOperation, M <: ProductManifold}
    return ManifoldsBase.check_size(PrG.manifold, identity_element(PrG, typeof(X)), X)
end

function conjugate!(
        PrG::LieGroup{ğ”½, Op, M}, k, g, h
    ) where {ğ”½, Op <: ProductGroupOperation, M <: ProductManifold}
    PrM = PrG.manifold
    map(
        conjugate!,
        map(LieGroup, PrM.manifolds, PrG.op.operations),
        submanifold_components(PrM, k),
        submanifold_components(PrM, g),
        submanifold_components(PrM, h),
    )
    return k
end

@doc raw"""
    cross(G::LieGroup, H::LieGroup)
    G Ã— H
    G1 Ã— G2 Ã— G3 Ã— ...

Return the [`ProductLieGroup`](@ref) For two [`LieGroups`](@ref) `G` and `H`.

For the case that one of them is a product Lie group already,
the other one is appended or prepended, depending on which one is the product;
they are joined into one large product if both are product Lie groups.

In order to build a [`ProductLieGroup`](@ref) that does not â€œsplatâ€ its arguments,
or in other words to obtain â€œnestedâ€ products,
use [`ProductLieGroup`](@ref)`(G1, G2, G3, ...)`.

# Example.

For
```
G1 = TranslationGroup(2)
G2 = SpecialOrthogonalGroup(2)
G3 = GeneralLinearGroup(2)
```

We can have one large product Lie group

```
G = G1 Ã— G2 Ã— G3 # or equivalently ProductLieGroup(G1, G2, G3)
```

and alternatively generate a product of a Lie group with an existing product using

```
H = ProductLieGroup(G1, G2 Ã— G3)
```

!!! note "Technical detail"
    Since for the first, single Lie group, the order should be irrelevant, it means
    in practice that `Ã—` behaves slightly different than `ProductLieGroup` in that it â€œsplatsâ€ its arguments.
    `G` is equivalent to calling
    `ProductLieGroup(G1, G2) Ã— G3` or ` G1 Ã— ProductLieGroup(G2, G3)`.
    Both, as `G` would consist of vectors of length 3.
    These are different from both
    `ProductLieGroup(ProductLieGroup(G1, G2), G3)` and `ProductLieGroup(G1, ProductLieGroup(G2, G3))`,
    which are both vectors of length 2, where the first has a vector of length 2 in its first component,
    the second such a vector in its second component.
"""
LinearAlgebra.cross(::LieGroup...)
function LinearAlgebra.cross(G::LieGroup, H::LieGroup)
    return ProductLieGroup(G, H)
end
function LinearAlgebra.cross(
        G::LieGroup{ğ”½, Op, M}, H::LieGroup
    ) where {ğ”½, Op <: ProductGroupOperation, M <: ProductManifold}
    return ProductLieGroup(map(LieGroup, G.manifold.manifolds, G.op.operations)..., H)
end
function LinearAlgebra.cross(
        G::LieGroup, H::LieGroup{ğ”½, Op, M}
    ) where {ğ”½, Op <: ProductGroupOperation, M <: ProductManifold}
    return ProductLieGroup(G, map(LieGroup, H.manifold.manifolds, H.op.operations)...)
end
function LinearAlgebra.cross(
        G::LieGroup{ğ”½1, Op1, M1}, H::LieGroup{ğ”½2, Op2, M2}
    ) where {ğ”½1, Op1 <: ProductGroupOperation, M1 <: ProductManifold, ğ”½2, Op2 <: ProductGroupOperation, M2 <: ProductManifold}
    return ProductLieGroup(
        map(LieGroup, G.manifold.manifolds, G.op.operations)...,
        map(LieGroup, H.manifold.manifolds, H.op.operations)...
    )
end

function diff_conjugate!(
        PrG::LieGroup{ğ”½, Op, M}, Y, g, h, X
    ) where {ğ”½, Op <: ProductGroupOperation, M <: ProductManifold}
    PrM = PrG.manifold
    map(
        diff_conjugate!,
        LieGroup.(PrM.manifolds, PrG.op.operations),
        submanifold_components(PrG, Y),
        submanifold_components(PrG, g),
        submanifold_components(PrG, h),
        submanifold_components(PrG, X),
    )
    return Y
end

function diff_inv!(
        PrG::LieGroup{ğ”½, Op, M}, Y, g, X
    ) where {ğ”½, Op <: ProductGroupOperation, M <: ProductManifold}
    PrM = PrG.manifold
    map(
        diff_inv!,
        LieGroup.(PrM.manifolds, PrG.op.operations),
        submanifold_components(PrG, Y),
        submanifold_components(PrG, g),
        submanifold_components(PrG, X),
    )
    return Y
end

function diff_left_compose!(
        PrG::LieGroup{ğ”½, Op, M}, Y, g, h, X
    ) where {ğ”½, Op <: ProductGroupOperation, M <: ProductManifold}
    PrM = PrG.manifold
    map(
        diff_left_compose!,
        LieGroup.(PrM.manifolds, PrG.op.operations),
        submanifold_components(PrG, Y),
        submanifold_components(PrG, g),
        submanifold_components(PrG, h),
        submanifold_components(PrG, X),
    )
    return Y
end

function diff_right_compose!(
        PrG::LieGroup{ğ”½, Op, M}, Y, g, h, X
    ) where {ğ”½, Op <: ProductGroupOperation, M <: ProductManifold}
    PrM = PrG.manifold
    map(
        diff_right_compose!,
        LieGroup.(PrM.manifolds, PrG.op.operations),
        submanifold_components(PrG, Y),
        submanifold_components(PrG, g),
        submanifold_components(PrG, h),
        submanifold_components(PrG, X),
    )
    return Y
end

function ManifoldsBase.exp!(
        PrG::LieGroup{ğ”½, Op, M}, h, X
    ) where {ğ”½, Op <: ProductGroupOperation, M <: ProductManifold}
    PrM = PrG.manifold
    map(
        (M, h, X) -> exp!(M, h, X), # introduce a function with â€œhard codedâ€ t
        map(LieGroup, PrM.manifolds, PrG.op.operations),
        submanifold_components(PrM, h),
        submanifold_components(PrM, X),
    )
    return h
end

function ManifoldsBase.exp!(
        PrG::LieGroup{ğ”½, Op, M}, h, g, X
    ) where {ğ”½, Op <: ProductGroupOperation, M <: ProductManifold}
    PrM = PrG.manifold
    map(
        (M, h, g, X) -> exp!(M, h, g, X), # introduce a function with â€œhard codedâ€ t
        map(LieGroup, PrM.manifolds, PrG.op.operations),
        submanifold_components(PrM, h),
        submanifold_components(PrM, g),
        submanifold_components(PrM, X),
    )
    return h
end

function get_vector_lie!(
        Prğ”¤::LieAlgebra{ğ”½, Op, LieGroup{ğ”½, Op, M}}, X, c, B::DefaultLieAlgebraOrthogonalBasis
    ) where {ğ”½, Op <: AbstractProductGroupOperation, M <: ProductManifold}
    PrG = Prğ”¤.manifold
    PrM = PrG.manifold
    dims = map(manifold_dimension, PrM.manifolds)
    @assert length(c) == sum(dims)
    dim_ranges = ManifoldsBase._get_dim_ranges(dims)
    Prc = map(dr -> (@inbounds view(c, dr)), dim_ranges)
    PrL = LieAlgebra.(LieGroup.(PrM.manifolds, PrG.op.operations))
    ts = ManifoldsBase.ziptuples(PrL, submanifold_components(PrM, X), Prc)
    map(ts) do t
        return get_vector_lie!(t..., B)
    end
    return X
end

@inline Base.getindex(pgo::ProductGroupOperation, i::Integer) = pgo.operations[i]
@inline Base.getindex(pgo::ProductGroupOperation, ::Colon) = pgo.operations

function identity_element!(
        PrG::LieGroup{ğ”½, Op, M}, e
    ) where {ğ”½, Op <: ProductGroupOperation, M <: ProductManifold}
    PrM = PrG.manifold
    map(
        identity_element!,
        map(LieGroup, PrM.manifolds, PrG.op.operations),
        submanifold_components(PrM, e),
    )
    return e
end

function ManifoldsBase.inner(
        Prğ”¤::LieAlgebra{ğ”½, Op, LieGroup{ğ”½, Op, M}}, X, Y
    ) where {ğ”½, Op <: AbstractProductGroupOperation, M <: ProductManifold}
    PrG = Prğ”¤.manifold # The product Lie group
    PrM = PrG.manifold # The product manifold
    return sum(
        map(
            inner,
            LieAlgebra.(map(LieGroup, PrM.manifolds, PrG.op.operations)),
            submanifold_components(PrM, X),
            submanifold_components(PrM, Y),
        ),
    )
end

function _inv!(
        PrG::LieGroup{ğ”½, Op, M}, h, g
    ) where {ğ”½, Op <: ProductGroupOperation, M <: ProductManifold}
    PrM = PrG.manifold
    map(
        inv!,
        map(LieGroup, PrM.manifolds, PrG.op.operations),
        submanifold_components(PrM, h),
        submanifold_components(PrM, g),
    )
    return h
end
function Manifolds.inv!(
        PrG::LieGroup{ğ”½, Op, M}, h, ::Identity{Op}
    ) where {ğ”½, Op <: ProductGroupOperation, M <: ProductManifold}
    PrM = PrG.manifold
    map(
        inv!,
        map(LieGroup, PrM.manifolds, PrG.op.operations),
        submanifold_components(PrM, h),
        map(Identity, PrG.op.operations),
    )
    return h
end

function lie_bracket!(
        PrA::LieAlgebra{ğ”½, Op, <:LieGroup{ğ”½, Op, M}}, Z, X, Y
    ) where {ğ”½, Op <: ProductGroupOperation, M <: ProductManifold}
    PrM = PrA.manifold.manifold
    map(
        lie_bracket!,
        map(LieAlgebra, LieGroup.(PrM.manifolds, PrA.manifold.op.operations)),
        submanifold_components(PrM, Z),
        submanifold_components(PrM, X),
        submanifold_components(PrM, Y),
    )
    return Z
end

function ManifoldsBase.log!(
        PrG::LieGroup{ğ”½, Op, M}, X, ::Identity{Op}
    ) where {ğ”½, Op <: ProductGroupOperation, M <: ProductManifold}
    zero_vector!(LieAlgebra(PrG), X)
    return X
end
function ManifoldsBase.log!(
        PrG::LieGroup{ğ”½, Op, M}, X, ::Identity{Op}, ::Identity{Op}
    ) where {ğ”½, Op <: ProductGroupOperation, M <: ProductManifold}
    zero_vector!(LieAlgebra(PrG), X)
    return X
end
function ManifoldsBase.log!(
        PrG::LieGroup{ğ”½, Op, M}, X, g, h
    ) where {ğ”½, Op <: ProductGroupOperation, M <: ProductManifold}
    PrM = PrG.manifold
    map(
        log!,
        map(LieGroup, PrM.manifolds, PrG.op.operations),
        submanifold_components(PrM, X),
        submanifold_components(PrM, g),
        submanifold_components(PrM, h),
    )
    return X
end

function ManifoldsBase.log!(
        PrG::LieGroup{ğ”½, Op, M}, X, h
    ) where {ğ”½, Op <: ProductGroupOperation, M <: ProductManifold}
    PrM = PrG.manifold
    map(
        log!,
        map(LieGroup, PrM.manifolds, PrG.op.operations),
        submanifold_components(PrM, X),
        submanifold_components(PrM, h),
    )
    return X
end

function Base.show(
        io::IO, G::LieGroup{ğ”½, Op, M}
    ) where {ğ”½, Op <: ProductGroupOperation, M <: ProductManifold}
    PrM = G.manifold.manifolds
    ops = G.op.operations
    return print(io, "ProductLieGroup($(join(PrM, " Ã— ")), $(join(ops, " Ã— ")))")
end

function get_coordinates_lie!(
        Prğ”¤::LieAlgebra{ğ”½, Op, LieGroup{ğ”½, Op, M}}, c, X, B::DefaultLieAlgebraOrthogonalBasis
    ) where {ğ”½, Op <: AbstractProductGroupOperation, M <: ProductManifold}
    PrG = Prğ”¤.manifold
    PrM = PrG.manifold
    dims = map(manifold_dimension, PrM.manifolds)
    @assert length(c) == sum(dims)
    dim_ranges = ManifoldsBase._get_dim_ranges(dims)
    Prc = map(dr -> (@inbounds view(c, dr)), dim_ranges)
    PrL = LieAlgebra.(LieGroup.(PrM.manifolds, PrG.op.operations))
    ts = ManifoldsBase.ziptuples(PrL, Prc, submanifold_components(PrM, X))
    map(ts) do t
        return get_coordinates_lie!(t..., B)
    end
    return c
end

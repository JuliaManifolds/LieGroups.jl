#
#
# Group Action related types and functions

@doc """
    AbstractGroupActionType

An abstract supertype for group action types, which are used within a [`GroupAction`](@ref).
"""
abstract type AbstractGroupActionType end

@doc """
    AbstractLeftGroupActionType <: AbstractGroupActionType

A type representing a (smooth) group action ``Ïƒ: $(_math(:G)) Ã— $(_math(:M)) â†’ $(_math(:M))``
of a [`AbstractLieGroup`](@ref) ``$(_math(:G))`` acting (from the left) on an $(_link(:AbstractManifold)) ``$(_math(:M))``.
with the following properties

1. ``Ïƒ($(_math(:e)), p) = p`` holds for all ``p âˆˆ $(_math(:M))``
2. ``Ïƒ(g, Ïƒ(h, p)) = Ïƒ(g$(_math(:âˆ˜))h, p)`` holds for all ``g,h âˆˆ $(_math(:G))``, ``p âˆˆ $(_math(:M))``


where ``$(_math(:âˆ˜))`` denotes the group operation of the [`AbstractLieGroup`](@ref) ``$(_math(:G))``.
See also [HilgertNeeb:2012; Definition 9.1.11](@cite).

The type of action can be seen a bit better when writing the action as a family ``Ïƒ_g(p)``:
we obtain from the second property as

```math
  Ïƒ_g(Ïƒ_h(p)) = Ïƒ_{gh}(p)
```

and see that ``g`` appears on the left.

When writing about general group actions, the symbol ``Î±`` is often used. The order of arguments then follows the same as the one of the left action.
Most often we use the index notation ``Î±_g(p)``.
The definition of functions also follows this notation, i.e. we use e.g. `apply(A, g, p)`

One notable example of a left action is the inverse of an action of [`AbstractRightGroupActionType`](@ref) ``Ï„``,
which is given by ``Ïƒ_g = (Ï„_g)^{-1} = Ï„_{g^{-1}}``.
We obtain

$(_note(:RightInverseActionIsLeft))
"""
abstract type AbstractLeftGroupActionType <: AbstractGroupActionType end

@doc """
    AbstractRightGroupActionType <: AbstractGroupActionType

A type representing a (smooth) group action ``Ï„: $(_math(:M)) Ã— $(_math(:G)) â†’ $(_math(:M))``
of a [`AbstractLieGroup`](@ref) ``$(_math(:G))`` acting (from the right) on an $(_link(:AbstractManifold)) ``$(_math(:M))``.
with the following properties

1. ``Ï„(p, $(_math(:e))) = p`` holds for all ``p âˆˆ $(_math(:M))``
2. ``Ï„(Ï„(p, g), h) = Ï„(Ï„(p, g), h)`` holds for all ``g,h âˆˆ $(_math(:G))``, ``p âˆˆ $(_math(:M))``

where ``$(_math(:âˆ˜))`` denotes the group operation of the [`AbstractLieGroup`](@ref) ``$(_math(:G))``.
See also [HilgertNeeb:2012; Remark 9.1.12](@cite).

The type of action can be seen a bit better when writing the action as a family ``Ï„_g(p)``:
we obtain from the second property as

```math
  Ï„_g(Ï„_h(p)) = Ï„_{hg}(p)
```

and see that ``g`` appears on the right.

When writing about general group actions, the symbol ``Î±`` is often used.
In that case the order of arguments follows either the one from the left action, but most often we use the index notation.
The definition of functions also follows this notation, i.e. we use e.g. `apply(A, g, p)`

One notable example of a right action is the inverse of an action of [`AbstractLeftGroupActionType`](@ref) ``Ïƒ``,
which is given by ``Ï„_g = (Ïƒ_g)^{-1} = Ïƒ_{g^{-1}}``.
We obtain

$(_note(:LeftInverseActionIsRight))
"""
abstract type AbstractRightGroupActionType <: AbstractGroupActionType end

"""
    AbstractActionActsOnType

An abstract type representing what an action acts on,
Most notably these are the [`ActionActsOnLeft`](@ref) and [`ActionActsOnRight`](@ref),
see their documentations for more details.

For its practical use see the [`SemidirectProductGroupOperation`](@ref).
"""
abstract type AbstractActionActsOnType end

"""
    ActionActsOnLeft <: AbstractActionActsOnType

An [`AbstractActionActsOnType`](@ref) representing that an action acts on the left.

This is meant in the following way: Given a [`GroupAction`](@ref) ``Î±: $(_math(:G)) Ã— $(_tex(:Cal, "H")) â†’ $(_tex(:Cal, "H"))``
where a Lie group ``$(_math(:G))`` acts on another Lie group ``$(_tex(:Cal, "H"))`` with an arbitrary action.

Then, e.g. within the definition of the [`LeftSemidirectProductGroupOperation`](@ref) or [`RightSemidirectProductGroupOperation`](@ref),
we have two choices where the group action ``Î±`` acts on, namely:
Let ``g âˆˆ $(_math(:G))`` and ``h_1,h_2 âˆˆ $(_tex(:Cal, "H"))`` be given, then this type represents the variant

```math
Î±_g(h_1) â‹… h_2,
```
where ``â‹…`` denotes the group operation on ``$(_tex(:Cal, "H"))``.
The `Left` in the name of this type refers to the fact that the action is applied to the left element ``h_1``.

Note that this is independent of both the type of action (left or right) and whether the semidirect product is a left or a right semidirect one.

For its practical use see the [`SemidirectProductGroupOperation`](@ref).
"""
struct ActionActsOnLeft <: AbstractActionActsOnType end

"""
    ActionActsOnRight <: AbstractActionActsOnType

An [`AbstractActionActsOnType`](@ref) representing that an action acts on the right.

This is meant in the following way: Given a [`GroupAction`](@ref) ``Î±: $(_math(:G)) Ã— $(_tex(:Cal, "H")) â†’ $(_tex(:Cal, "H"))``
where a Lie group ``$(_math(:G))`` acts on another Lie group ``$(_tex(:Cal, "H"))`` with an arbitrary action.

Then, e.g. within the definition of the [`LeftSemidirectProductGroupOperation`](@ref) or [`RightSemidirectProductGroupOperation`](@ref),
we have two choices where the group action ``Î±`` acts on, namely:
Let ``g âˆˆ $(_math(:G))`` and ``h_1,h_2 âˆˆ $(_tex(:Cal, "H"))`` be given, then this type represents the variant

```math
h_1 â‹… Î±_g(h_2),
```
where ``â‹…`` denotes the group operation on ``$(_tex(:Cal, "H"))``.
The `Right` in the name of this type refers to the fact that the action is applied to the right element ``h_2``.

Note that this is independent of both the type of action (left or right) and whether the semidirect product is a left or a right semidirect one.

For its practical use see the [`SemidirectProductGroupOperation`](@ref).
"""
struct ActionActsOnRight <: AbstractActionActsOnType end

"""
    GroupAction{T<:GroupActionType, L<:LieGroup, M<:AbstractManifold}

Specify a group action of [`AbstractGroupActionType`](@ref) `T` of a [`AbstractLieGroup`](@ref) `G`
acting on an $(_link(:AbstractManifold)) `M`.

Let ``$(_math(:M))`` be a $(_link(:AbstractManifold)) and ``$(_math(:G))`` be a [`AbstractLieGroup`](@ref)
with group operation ``$(_math(:âˆ˜))``.

A (smooth) action of the group ``$(_math(:G))`` on the manifold ``$(_math(:M))`` is a map

```math
Î±: $(_math(:G)) Ã— $(_math(:M)) â†’ $(_math(:M))
```

with the properties

**Identity.** ``Î±($(_math(:e)), p) = p`` holds for all ``p âˆˆ $(_math(:M))``

**Compatibility.**
If ``Î±`` is a [``AbstractLeftGroupActionType``](@ref) we usually denote it by ``Ïƒ`` and the compatibility reads

```math
Ïƒ_g(Ïƒ_h(p)) = Ïƒ_{g$(_math(:âˆ˜))h}(p) $(_tex(:text, " holds for all")) g,h âˆˆ $(_math(:G)) $(_tex(:text, " and")) p âˆˆ $(_math(:M))
```

If ``Î±`` is a [``AbstractRightGroupActionType``](@ref) we usually denote it by ``Ï„`` and the compatibility reads

```math
Ï„_g(Ï„_h(p)) = Ï„_{h$(_math(:âˆ˜))g}(p)`` holds for all ``g,h âˆˆ $(_math(:G)) $(_tex(:text, " for all")) p âˆˆ $(_math(:M))
```

_intuitively_ the left/right property of an action specifies on which side the â€œouterâ€ group actions element ``g``
gets â€œappendedâ€ in the composition.

# Fields

* `type::`[`AbstractGroupActionType`](@ref): The type of the group action.
* `group::`[`AbstractLieGroup`](@ref): The group acting.
* `manifold::`$(_link(:AbstractManifold)): The manifold the group acts upon.

See [HilgertNeeb:2012; Section 9.1.3](@cite) for more details.

# Constructors
    GroupAction(
        group::AbstractLieGroup, manifold::ManifoldsBase.AbstractManifold, type::AbstractGroupActionType
    )
Generate a group action where the type of the action and what it acts on are keyword arguments.
They default to the most common choice, that the [`ActionActsOnRight`](@ref).
"""
struct GroupAction{
        T <: AbstractGroupActionType, L <: AbstractLieGroup, M <: ManifoldsBase.AbstractManifold,
    }
    type::T
    group::L
    manifold::M
end
function GroupAction(
        group::G, manifold::M, type::T
    ) where {G <: AbstractLieGroup, M <: ManifoldsBase.AbstractManifold, T <: AbstractGroupActionType}
    return GroupAction{T, G, M}(type, group, manifold)
end
#
#
# Functions

_doc_apply = """
    apply(A::GroupAction{T}, g, p)
    apply!(A::GroupAction{T}, q, g, p)

Apply the group action induced by ``g âˆˆ $(_math(:G))`` to ``p âˆˆ $(_math(:M))``,
where the kind of group action is indicated by the [`AbstractGroupActionType`](@ref) `T`.
This can be performed in-place of `q`.
"""

function apply end
@doc "$(_doc_apply)"
function apply(A::GroupAction, g, p)
    q = allocate_result(base_manifold(A), apply, p, g)
    apply!(A, q, g, p)
    return q
end

function apply! end
@doc "$(_doc_apply)"
apply!(A::GroupAction, q, g, p)

function base_lie_group end
@doc """
    base_lie_group(A::GroupAction)

Return the [`AbstractLieGroup`](@ref) of the [`GroupAction`](@ref)
specifying the action.
"""
base_lie_group(A::GroupAction) = A.group

@doc """
    base_manifold(A::GroupAction)

Return the $(_link(:AbstractManifold)) the group action acts upon.
"""
ManifoldsBase.base_manifold(A::GroupAction) = A.manifold

function default_left_action end
"""
    default_left_action(G::AbstractLieGroup, M::AbstractManifold)

Return the default left action for a Lie group `G` acting on a manifold `M`.
"""
default_left_action(N::AbstractLieGroup, M::AbstractManifold)

function default_right_action end
"""
    default_right_action(G::AbstractLieGroup, M::AbstractManifold)

Return the default right action for a Lie group `G` acting on a manifold `M`.
"""
default_right_action(N::AbstractLieGroup, M::AbstractManifold)

_doc_diff_apply = """
    diff_apply(A::GroupAction, g, p, X)
    diff_apply!(A::GroupAction, Y, g, p, X)

Compute the differential ``$(_math(:D))_p Î±_g(p): T_p$(_math(:M)) â†’ T_{Ïƒ_g(p)}$(_math(:M))``.
"""

function diff_apply end
@doc "$(_doc_diff_apply)"
function diff_apply(A::GroupAction, g, p, X)
    Y = allocate_result(base_manifold(A), diff_apply, p, g, X)
    diff_apply!(A, Y, g, p, X)
    return Y
end

function diff_apply! end
@doc "$(_doc_diff_apply)"
diff_apply!(A::GroupAction, q, g, p)

_doc_diff_group_apply = """
    diff_group_apply(A::GroupAction, g, p, X)
    diff_group_apply!(A::GroupAction, Y, g, p, X)

Compute the differential ``$(_math(:d))_{$(_tex(:Cal, "G"))} Ïƒ_g(p): $(_math(:ð”¤)) â†’ $(_math(:ð”¤))``,
where we use the short hand notation ``Ïƒ_p(g) = Ïƒ(g,p)`` for a left action,
and for a right action ``Ï„_p(g) = Ï„(g,p)``.
"""

function diff_group_apply end
@doc "$(_doc_diff_group_apply)"
function diff_group_apply(A::GroupAction, g, p, X)
    Y = allocate_result(base_manifold(A), apply, g, p, X)
    diff_group_apply!(A, Y, g, p, X)
    return Y
end

function diff_group_apply! end
@doc "$(_doc_diff_group_apply)"
diff_group_apply!(A::GroupAction, q, g, p)

@doc """
     inv(A::GroupAction{T})

Return the [`GroupAction`](@ref) representing the inverse of an [`GroupAction`](@ref) of [`AbstractGroupActionType`](@ref) `T`.
This is usually done by returning the group action with the inverse type of `T`.
"""
Base.inv(A::GroupAction) = GroupAction(A.group, A.manifold, inv(A.type))

"""
    inv(::AbstractGroupActionType)

return the inverse group operation action, that is, use the type representing the
inverse operation.
"""
Base.inv(::AbstractGroupActionType)

function Base.show(io::IO, A::GroupAction)
    return print(io, "GroupAction($(A.group), $(A.manifold), $(A.type))")
end

function switch end

@doc """
     switch(A::GroupAction{T})

Return the group operation action representing the similar [`GroupAction`](@ref) of [`AbstractGroupActionType`](@ref) `T`
but acting from the other side. It switches left to right and vice versa.
This is done by returning the group action with the â€œswitchedâ€ type of `T`.
"""
switch(A::GroupAction) = GroupAction(A.group, A.manifold, switch(A.type))

@doc """
    switch(T::AbstractGroupActionType)

Return the object representing an [`AbstractGroupActionType`](@ref) related to a group operation action that switched the side,
that is it turns a left action type into its corresponding right action type.
"""
switch(::AbstractGroupActionType)
